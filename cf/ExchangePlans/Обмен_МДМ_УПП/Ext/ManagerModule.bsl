Процедура ВыполнитьРегистрациюДляУПП(Ссылка, РегистрацияПолногоНабора = Ложь) экспорт

	ПринадлежитУПП = Метаданные.ПланыОбмена.Обмен_МДМ_УПП.Состав.Содержит(Ссылка.Метаданные());
	
	Если НЕ ПринадлежитУПП Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаУзловОбмена = ПолучитьУзлыОбменаУПП(Ссылка);
	
	Если ТаблицаУзловОбмена.Количество() Тогда
		
		УзлыОбменаРегистрация = ПолучитьМассивУзлов(ТаблицаУзловОбмена, Истина);
		
		Если УзлыОбменаРегистрация.Количество() Тогда
			
			ПолнаяСинхронизация = ПолнаяСинхронизация(Ссылка);
			
			Если ПолнаяСинхронизация Тогда
				ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбменаРегистрация, Ссылка);
			Иначе
				ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбменаРегистрация, Ссылка);
			КонецЕсли; 
			
			Если НЕ ПолнаяСинхронизация 
				ИЛИ (ПолнаяСинхронизация И РегистрацияПолногоНабора) Тогда
				РегистрацияСправочниковВладельца(Ссылка, УзлыОбменаРегистрация, ПолнаяСинхронизация);
			КонецЕсли; 
			
			РегистрацияКонтактнойИнформации(Ссылка, УзлыОбменаРегистрация, ПолнаяСинхронизация);			
			
		КонецЕсли; 
		
		УзлыОбменаУдаление = ПолучитьМассивУзлов(ТаблицаУзловОбмена, Ложь);
		
		Если УзлыОбменаУдаление.Количество() Тогда
			
			ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбменаУдаление, Ссылка);
			
			РегистрацияСправочниковВладельца(Ссылка, УзлыОбменаУдаление);
			
			РегистрацияКонтактнойИнформации(Ссылка, УзлыОбменаУдаление);
			
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьМассивУзлов(ТаблицаУзловОбмена, Обмен)

	Отбор = Новый Структура;
	Отбор.Вставить("Обмен",Обмен);
	СтрокиДляРегистрации = ТаблицаУзловОбмена.НайтиСтроки(Отбор);
	МассивУзлов = Новый Массив;
	Для каждого СтрокаДляРегистрации Из СтрокиДляРегистрации Цикл
		МассивУзлов.Добавить(СтрокаДляРегистрации.Узел);
	КонецЦикла;
	
	Возврат МассивУзлов;

КонецФункции // ПолучитьМассивУзлов()

Функция ПолнаяСинхронизация(Ссылка)
	
	СсылкаДляОбработки = Ссылка;
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
		СсылкаДляОбработки = Ссылка.Владелец;
	КонецЕсли;
	
	ЭлементСостава = Метаданные.ОбщиеРеквизиты.ПолнаяСинхронизацияMDM.Состав.Найти(СсылкаДляОбработки.Метаданные());
	
	Использование = ЭлементСостава.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Использовать;
	
	Возврат НЕ Использование
			ИЛИ (Использование И СсылкаДляОбработки.ПолнаяСинхронизацияMDM);
		
КонецФункции // СсылкаПолнаяСинхронизация()
 
Функция ПреобразоватьСсылкуПоВладельцу(Ссылка, ФункцияПолнаяСинхронизация = Ложь) Экспорт 

	ЕдиницыИзмерения=Тип("СправочникСсылка.ЕдиницыИзмерения");
	ПунктыРазгрузки=Тип("СправочникСсылка.алкПунктыРазгрузки");
	Лицензии=Тип("СправочникСсылка.алкЛицензии");
	БанковскиеСчета=Тип("СправочникСсылка.нсиБанковскиеСчета");
	Договоры=Тип("СправочникСсылка.ДоговорыКонтрагентов");
	Спецификации=Тип("СправочникСсылка.СпецификацииНоменклатуры");
	КонтактнаяИнформация = Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация");
	
	СсылкаКопия = Ссылка;
	
	Если ТипЗнч(Ссылка)=ЕдиницыИзмерения 
		ИЛИ ТипЗнч(Ссылка)=ПунктыРазгрузки 
		ИЛИ ТипЗнч(Ссылка)=Лицензии 
		ИЛИ ТипЗнч(Ссылка)=БанковскиеСчета 
		Тогда
		СсылкаКопия=Ссылка.Владелец;    
	ИначеЕсли ТипЗнч(Ссылка)=Спецификации
		И ФункцияПолнаяСинхронизация Тогда 
		СсылкаКопия=Ссылка.Номенклатура;        
	ИначеЕсли ТипЗнч(Ссылка)=КонтактнаяИнформация Тогда 
		СсылкаКопия=ПреобразоватьСсылкуПоВладельцу(Ссылка.Отбор.Объект.Значение);        
	ИначеЕсли ТипЗнч(Ссылка)=Договоры 
		И ФункцияПолнаяСинхронизация Тогда 
		СсылкаКопия=Ссылка.Владелец;    
	КонецЕсли;
	
	Возврат СсылкаКопия; 

КонецФункции // ПреобразоватьСсылкуПоВладельцу()

Функция ПолучитьУзлыОбменаУПП(ОбъектОбмена) Экспорт 

	СсылкаВладелец = ПреобразоватьСсылкуПоВладельцу(ОбъектОбмена);
	
	Запрос=Новый Запрос;
	Если ТипЗнч(СсылкаВладелец) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		ИЛИ ТипЗнч(СсылкаВладелец) = Тип("СправочникСсылка.СпецификацииНоменклатуры") Тогда
		
		Запрос.Текст="ВЫБРАТЬ
		             |	УчастникиОбмена.УзелОбмена,
		             |	УчастникиОбмена.Обмен
		             |ПОМЕСТИТЬ УчастникиОбменаВладелец
		             |ИЗ
		             |	РегистрСведений.УчастникиОбмена КАК УчастникиОбмена
		             |ГДЕ
		             |	УчастникиОбмена.ОбъектОбмена = &ОбъектОбменаВладелец
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	УчастникиОбмена.УзелОбмена,
		             |	УчастникиОбмена.Обмен
		             |ПОМЕСТИТЬ УчастникиОбмена
		             |ИЗ
		             |	РегистрСведений.УчастникиОбмена КАК УчастникиОбмена
		             |ГДЕ
		             |	УчастникиОбмена.ОбъектОбмена = &ОбъектОбмена
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	ЕСТЬNULL(УчастникиОбмена.Обмен, ЛОЖЬ)
		             |		И УчастникиОбменаВладелец.Обмен КАК Обмен,
		             |	УчастникиОбменаВладелец.УзелОбмена КАК Узел
		             |ИЗ
		             |	УчастникиОбменаВладелец КАК УчастникиОбменаВладелец
		             |		ЛЕВОЕ СОЕДИНЕНИЕ УчастникиОбмена КАК УчастникиОбмена
		             |		ПО УчастникиОбменаВладелец.УзелОбмена = УчастникиОбмена.УзелОбмена";
		
		Если ТипЗнч(СсылкаВладелец) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			ИмяРеквизита = "Владелец";
		ИначеЕсли ТипЗнч(СсылкаВладелец) = Тип("СправочникСсылка.СпецификацииНоменклатуры") Тогда
			ИмяРеквизита = "Номенклатура";
		КонецЕсли; 
		Запрос.УстановитьПараметр("ОбъектОбменаВладелец", СсылкаВладелец[ИмяРеквизита]);
	Иначе
		
		Запрос.Текст="ВЫБРАТЬ
		             |	УчастникиОбмена.УзелОбмена КАК Узел,
		             |	УчастникиОбмена.Обмен
		             |ИЗ
		             |	РегистрСведений.УчастникиОбмена КАК УчастникиОбмена
		             |ГДЕ
		             |	УчастникиОбмена.ОбъектОбмена = &ОбъектОбмена"
		
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ОбъектОбмена", СсылкаВладелец);
	Результат = Запрос.Выполнить().Выгрузить();
	
	УдалитьУзелSAP(Результат, ОбъектОбмена);
	
	Возврат Результат;

КонецФункции // ПолучитьУзлыОбмена()

Процедура УдалитьУзелSAP(ТаблицаУзлов, Ссылка) Экспорт 
	
	ЕдиницыИзмерения=Тип("СправочникСсылка.ЕдиницыИзмерения");
	Договоры=Тип("СправочникСсылка.ДоговорыКонтрагентов");
	Спецификации=Тип("СправочникСсылка.СпецификацииНоменклатуры");
	КонтактнаяИнформация = Тип("РегистрСведенийНаборЗаписей.КонтактнаяИнформация");
	
	Если ТипЗнч(Ссылка)=ЕдиницыИзмерения 
		ИЛИ ТипЗнч(Ссылка)=Договоры 
		ИЛИ ТипЗнч(Ссылка)=КонтактнаяИнформация 
		ИЛИ ТипЗнч(Ссылка)=Спецификации Тогда
		
		Если ТаблицаУзлов.Количество() Тогда
			УзелSAP = УзелSAP();
			Если ЗначениеЗаполнено(УзелSAP) Тогда
				НайденнаяСтрока = ТаблицаУзлов.Найти(УзелSAP,"Узел");
				Если НЕ НайденнаяСтрока = Неопределено Тогда
					ТаблицаУзлов.Удалить(НайденнаяСтрока);
				КонецЕсли;
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры

Функция УзелSAP() Экспорт 

	Возврат ПланыОбмена.Обмен_МДМ_УПП.НайтиПоРеквизиту("SAP", Истина);

КонецФункции // УзелSAP()
 
Процедура ВыполнитьРегистрациюКонтактнойИнформации(НаборЗаписейКИ, УзлыОбмена, Регистрация) Экспорт 

	ИзмеренияТЗ = НаборЗаписейКИ.Выгрузить(,"Объект, Вид, Тип");
	ИзмеренияТЗ.Свернуть("Объект, Вид, Тип");
	
	Для каждого СтрокаИзмерений Из ИзмеренияТЗ Цикл
		
		Для каждого КолонкаТЗ Из ИзмеренияТЗ.Колонки Цикл
			НаборЗаписейКИ.Отбор[КолонкаТЗ.Имя].Установить(СтрокаИзмерений[КолонкаТЗ.Имя]);
		КонецЦикла; 
		
		Если Регистрация Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, НаборЗаписейКИ);
		Иначе
			ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбмена, НаборЗаписейКИ);
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры // ВыполнитьРегистрациюКонтактнойИнформации(УзелОбмена, ОбъектОбмена, Обмен)()

Процедура РегистрацияКонтактнойИнформации(ОбъектОбмена, УзлыОбмена = Неопределено, Регистрация = Ложь) Экспорт 

	Если ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.нсиКонтрагенты")
		ИЛИ ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.Организации") 
		ИЛИ ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.алкПунктыРазгрузки") 
		Тогда	
		
		НаборЗаписейКИ = РегистрыСведений.КонтактнаяИнформация.СоздатьНаборЗаписей();
		НаборЗаписейКИ.Отбор.Объект.Установить(ОбъектОбмена);
		НаборЗаписейКИ.Прочитать();
		
		ВыполнитьРегистрациюКонтактнойИнформации(НаборЗаписейКИ, УзлыОбмена, Регистрация);
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьРегистрациюКонтактнойИнформации(УзелОбмена, ОбъектОбмена, Обмен)()

Процедура РегистрацияСправочниковВладельца(Владелец, УзлыОбмена = Неопределено, Регистрация = Ложь) Экспорт 

	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.нсиМТР") Тогда
		
		СправочникиВладельца = Новый Структура;
		СправочникиВладельца.Вставить("ЕдиницыИзмерения");
		СправочникиВладельца.Вставить("СпецификацииНоменклатуры");
		
	ИначеЕсли ТипЗнч(Владелец) = Тип("СправочникСсылка.нсиКонтрагенты")
		ИЛИ ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") 
		Тогда	
		
		СправочникиВладельца = Новый Структура;
		СправочникиВладельца.Вставить("алкЛицензии");
		СправочникиВладельца.Вставить("алкПунктыРазгрузки");
		СправочникиВладельца.Вставить("нсиБанковскиеСчета");
		
		Если ТипЗнч(Владелец) = Тип("СправочникСсылка.нсиКонтрагенты") Тогда
			СправочникиВладельца.Вставить("ДоговорыКонтрагентов");
		КонецЕсли;
	Иначе	
		Возврат;
	КонецЕсли;
	
	Для каждого СправочникВладельца Из СправочникиВладельца Цикл
		Если СправочникВладельца.Ключ = "СпецификацииНоменклатуры" Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	СпецификацииНоменклатуры.Ссылка
			|ИЗ
			|	Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
			|ГДЕ
			|	СпецификацииНоменклатуры.Номенклатура = &Номенклатура";
			
			Запрос.УстановитьПараметр("Номенклатура", Владелец);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Выборка = РезультатЗапроса.Выбрать();
			
		Иначе
			Выборка = Справочники[СправочникВладельца.Ключ].Выбрать(,Владелец);
		КонецЕсли; 
		
		//УсловиеСамостоятельнойРегистрации = (СправочникВладельца.Ключ = "СпецификацииНоменклатуры" 
		//									ИЛИ СправочникВладельца.Ключ = "ДоговорыКонтрагентов") И Регистрация;
					
		Пока Выборка.Следующий() Цикл
			
			Если СправочникВладельца.Ключ = "ЕдиницыИзмерения" Тогда
				Если Регистрация Тогда
					ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, Выборка.Ссылка);
				Иначе
					ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбмена, Выборка.Ссылка);
				КонецЕсли;
			Иначе
				ВыполнитьРегистрациюДляУПП(Выборка.Ссылка);
			КонецЕсли; 
			
			//Если УзлыОбмена = Неопределено ИЛИ УсловиеСамостоятельнойРегистрации Тогда
			//	ВыполнитьРегистрациюДляУПП(Выборка.Ссылка);
			//Иначе
			//	Если Регистрация Тогда
			//		ПланыОбмена.ЗарегистрироватьИзменения(УзлыОбмена, Выборка.Ссылка);
			//	Иначе
			//		ПланыОбмена.УдалитьРегистрациюИзменений(УзлыОбмена, Выборка.Ссылка);
			//	КонецЕсли;
			//	РегистрацияКонтактнойИнформации(Выборка.Ссылка, УзлыОбмена, Регистрация);
			//КонецЕсли; 
		
		КонецЦикла;
		
	КонецЦикла; 

КонецПроцедуры

