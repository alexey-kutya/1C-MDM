#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если Не ПараметрыВыполненияКоманды.Источник.Окно = Неопределено Тогда 
		Если Не ПараметрыВыполненияКоманды.Источник.Окно.Основное Тогда 
			ПараметрыВыполненияКоманды.Источник.Закрыть();
		КонецЕсли;
	КонецЕсли;	
	
	МассивЗадач = ПараметрКоманды;
	
	Если МассивЗадач = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("en='There are no selected task.';ru='Не выбраны задачи.'"));
		Возврат;
	КонецЕсли;
	
	ПричинаОтказа = "";
	ЗадачиМогутБытьПеренаправлены = нсиБизнесПроцессы.ПеренаправитьЗадачи(МассивЗадач, Неопределено, Истина,,ПричинаОтказа);
	Если НЕ ЗадачиМогутБытьПеренаправлены Тогда
		ПоказатьПредупреждение(,"Невозможно перенаправить задачу."+Символы.ПС+ПричинаОтказа);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаВыбораИсполнителя",ЭтотОбъект,МассивЗадач);
	
	ОткрытьФорму(
		"Задача.ЗадачаИсполнителя.Форма.ФормаВыбораИсполнителя",
		новый Структура("ДопустимыеИсполнители",ПолучитьИсполнителей()),,,,,
		ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс
	);
КонецПроцедуры

// Перенаправляет задачи определённому исполнителю.
//
// Параметры:
//    стрИсполнитель - Структура - структура с данными об исполнителе.
//    МассивЗадач - Массив - массив задач, которые необходимо перенаправить.
//
&НаКлиенте
Процедура ОбработкаВыбораИсполнителя(стрИсполнитель, МассивЗадач) Экспорт
	Если стрИсполнитель<>Неопределено Тогда 
		Если МассивЗадач.Количество() > 10 Тогда
			Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),, 
			НСтр("en='Redirecting tasks in progress...';ru='Выполняется перенаправление задач...'"));
		КонецЕсли;

		МассивПеренаправленныхЗадач = Неопределено;
		
		ПараметрыПеренаправления =  Новый Структура("Исполнитель,РольИсполнителя,ОсновнойОбъектАдресации,ДополнительныйОбъектАдресации,СпособРаспределения,Комментарий",
			стрИсполнитель.Исполнитель,
			Неопределено,
			Неопределено,
			Неопределено,
			Неопределено,
			стрИсполнитель.Комментарий
		);

		ЗадачиПеренаправлены = нсиБизнесПроцессы.ПеренаправитьЗадачи(МассивЗадач, ПараметрыПеренаправления, Ложь, МассивПеренаправленныхЗадач);
		Если МассивЗадач.Количество() > 1 Тогда
			Если ЗадачиПеренаправлены Тогда
				Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),, 
					НСтр("en='Tasks redirected.';ru='Задачи перенаправлены.'"));
			Иначе
				Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),,
					НСтр("en='Not all tasks redirected.';ru='Не все задачи перенаправлены.'"));
			КонецЕсли;
		Иначе
			Если МассивПеренаправленныхЗадач<>Неопределено Тогда 
				Задача = МассивПеренаправленныхЗадач[0];
				ПоказатьОповещениеПользователя(
					НСтр("en='The task is redirected';ru='Задача перенаправлена'"),
					ПолучитьНавигационнуюСсылку(Задача),
					Строка(Задача));
			Иначе
				ПоказатьОповещениеПользователя(НСтр("en='The task is not redirected';ru='Задача не перенаправлена'"));
			КонецЕсли;	
		КонецЕсли;
		Оповестить("ЗадачаИзменена", МассивЗадач);
		Оповестить("ЗадачаПеренаправлена", МассивЗадач);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьИсполнителей()
	Возврат нсиБизнесПроцессы.ИсполнителиРоли(Справочники.РолиИсполнителей.ОтветственныйЗаКонтрольИсполнения);
КонецФункции

#КонецОбласти
