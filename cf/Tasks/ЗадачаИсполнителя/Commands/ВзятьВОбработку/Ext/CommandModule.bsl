#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если Не ПараметрыВыполненияКоманды.Источник.Окно = Неопределено Тогда 
		Если Не ПараметрыВыполненияКоманды.Источник.Окно.Основное Тогда 
			ПараметрыВыполненияКоманды.Источник.Закрыть();
		КонецЕсли;
	КонецЕсли;	
	
	МассивЗадач = ПараметрКоманды;
	
	Если МассивЗадач = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("en='There are no selected task.';ru='Не выбраны задачи.'"));
		Возврат;
	КонецЕсли;
	
	ПричинаОтказа = "";
	ЗадачиМогутБытьПеренаправлены = нсиБизнесПроцессы.ПеренаправитьЗадачи(МассивЗадач, Неопределено, Истина,,ПричинаОтказа);
	Если НЕ ЗадачиМогутБытьПеренаправлены Тогда
//	ITRR Кутья АА добавлена локализация для английского интерфейса
//		ПоказатьПредупреждение(,"Невозможно перенаправить задачу."+Символы.ПС+ПричинаОтказа);
		ПоказатьПредупреждение(,НСтр("ru = 'Невозможно перенаправить задачу.'; en = 'Can not redirect the task.'")
		+Символы.ПС+ПричинаОтказа);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаОтветаПеренаправитьЗадачи", ЭтотОбъект, МассивЗадач);
	ПоказатьВопрос(
		ОписаниеОповещения,
//	ITRR Кутья АА добавлена локализация для английского интерфейса
//		"Вы уверены, что хотите перенаправить задачи себе?",
		НСтр("ru = 'Вы уверены, что хотите перенаправить задачи себе?
         |'; en = 'Are you sure you want to redirect the tasks to yourself?'"),
		РежимДиалогаВопрос.ДаНет,,
		КодВозвратаДиалога.Нет,
		НСтр("ru = 'Внимание'; en = 'Attention'")
	);
КонецПроцедуры

// Перенаправляет задачи.
//
// Параметры:
//    Результат - Вопрос - решение о перенаправлении задачи.
//    МассивЗадач - Массив - массив задач, которые необходимо перенаправить.
//
&НаКлиенте
Процедура ОбработкаОтветаПеренаправитьЗадачи(Результат,МассивЗадач) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		
		Если МассивЗадач.Количество() > 10 Тогда
			Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),, 
			НСтр("en='Redirecting tasks in progress...';ru='Выполняется перенаправление задач...'"));
		КонецЕсли;

		МассивПеренаправленныхЗадач = Неопределено;
		
		ПараметрыПеренаправления =  Новый Структура("Исполнитель,РольИсполнителя,ОсновнойОбъектАдресации,ДополнительныйОбъектАдресации,СпособРаспределения,Комментарий",
			ТекущийПользователь(),
			Неопределено,
			Неопределено,
			Неопределено,
			Неопределено,
			Неопределено
		);

		ЗадачиПеренаправлены = нсиБизнесПроцессы.ПеренаправитьЗадачи(МассивЗадач, ПараметрыПеренаправления, Ложь, МассивПеренаправленныхЗадач);
		Если МассивЗадач.Количество() > 1 Тогда
			Если ЗадачиПеренаправлены Тогда
				Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),, 
					НСтр("en='Tasks redirected.';ru='Задачи перенаправлены.'"));
			Иначе
				Состояние(НСтр("en='Redirection task';ru='Перенаправление задач'"),,
					НСтр("en='Not all tasks redirected.';ru='Не все задачи перенаправлены.'"));
			КонецЕсли;
		Иначе
			Если МассивПеренаправленныхЗадач<>Неопределено Тогда 
				Задача = МассивПеренаправленныхЗадач[0];
				ПоказатьОповещениеПользователя(
					НСтр("en='The task is redirected';ru='Задача перенаправлена'"),
					ПолучитьНавигационнуюСсылку(Задача),
					Строка(Задача));
			Иначе
				ПоказатьОповещениеПользователя(НСтр("en='The task is not redirected';ru='Задача не перенаправлена'"));
			КонецЕсли;	
		КонецЕсли;
		Оповестить("ЗадачаИзменена", МассивЗадач);
		Оповестить("ЗадачаПеренаправлена", МассивЗадач);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ТекущийПользователь()
	Возврат ПараметрыСеанса.ТекущийПользователь;
КонецФункции

#КонецОбласти
