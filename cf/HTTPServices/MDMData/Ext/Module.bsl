
Функция ВызватьМетодPOST(Запрос)
	
	DataType = Запрос.ПараметрыURL["DataType"];
	
	Body = Запрос.ПолучитьТелоКакСтроку();
	
	Попытка
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Body);
		BodyStructure = ПрочитатьJSON(ЧтениеJSON);
		ArrayID = BodyStructure.ID;
		
		Если НЕ ArrayID.Количество() Тогда
			ВызватьИсключение "";
		КонецЕсли; 
	Исключение
		Ответ = Новый HTTPСервисОтвет(400);
		Ответ.УстановитьТелоИзСтроки("Unexpected request body");
		Возврат Ответ;
	КонецПопытки;
	
	
	Если DataType = "products" Тогда
		СтрокаJSON = ПолучитьДанныеJSON(ArrayID);
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.Заголовки.Вставить("Content-Type", "application/json;charset=utf-8");
		Ответ.УстановитьТелоИзСтроки(СтрокаJSON);
	Иначе
		Ответ = Новый HTTPСервисОтвет(404);
		Ответ.УстановитьТелоИзСтроки("Unknown data type: '"+DataType+"'");
	КонецЕсли; 
	
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьДанныеJSON(МассивID)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	нсиМТР.Код КАК ID,
		|	ЕСТЬNULL(нсиМТР.LocalMaterialType.Код, """") КАК MaterialTypeID,
		|	ЕСТЬNULL(нсиМТР.LocalMaterialType.Наименование, """") КАК MaterialTypeDescription,
		|	ЕСТЬNULL(нсиМТР.LocalBrand.Код, """") КАК BrandID,
		|	ЕСТЬNULL(нсиМТР.LocalBrand.Наименование, """") КАК BrandDescription
		|ИЗ
		|	Справочник.нсиМТР КАК нсиМТР
		|ГДЕ
		|	нсиМТР.Код В(&МассивID)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ID";
	
	Запрос.УстановитьПараметр("МассивID", МассивID);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Колонки = РезультатЗапроса.Колонки;
	
	ТаблицаПродуктов = РезультатЗапроса.Выгрузить();
	
	Data = Новый Массив;
	Для каждого ID Из МассивID Цикл
		Entry = Новый Структура;
		СтрокаПродукта = ТаблицаПродуктов.Найти(ID, "ID");
		Если СтрокаПродукта = Неопределено Тогда
	    	Entry.Вставить("ID", ID);
	    	Entry.Вставить("Error", "Not found");
		Иначе
			Для каждого Колонка Из Колонки Цикл
		    	Entry.Вставить(Колонка.Имя, СтрокаПродукта[Колонка.Имя]);
			КонецЦикла; 
		КонецЕсли; 
		Data.Добавить(Entry);
	КонецЦикла; 
	
	ОтветJSON = Новый Структура;
	ОтветJSON.Вставить("Data", Data);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, ОтветJSON);
	
	Возврат ЗаписьJSON.Закрыть();

КонецФункции // ПолучитьДанныеJSON()
