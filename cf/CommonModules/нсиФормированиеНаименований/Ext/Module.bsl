
#Область ПрограммныйИнтерфейс

// Функция - Получить наименование по шаблону
// Параметры:
// Объект - объект или ссылка на элемент справочника нсиМТР или нсиУслуги.
// Возвращаемое значение:
// Структура  - структура с ключами "Наименование" и "ПолноеНаименование";
//
Функция ПолучитьНаименованиеПоШаблонуУниверсальноеХранилище(Объект, пОбъект, Класс, ТехническиеХарактеристики = Неопределено, ТекущиеНастройкиФормирования = Неопределено) Экспорт 

	Если ТехническиеХарактеристики = Неопределено Тогда 
		ТехническиеХарактеристики = Объект.ТехническиеХарактеристики;
	КонецЕсли;
	
	Результат = Новый Структура("Наименование, ПолноеНаименование","","");
	
	Если ТекущиеНастройкиФормирования = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ТипЭлемента,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ЭлементНаименования,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПрефиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПостфиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.Использование
			|ИЗ
			|	Справочник."+Класс.Метаданные().Имя+".НастройкиШаблоновНаименований КАК нсиКлассификаторНастройкиШаблоновНаименований
			|ГДЕ
			|	нсиКлассификаторНастройкиШаблоновНаименований.Ссылка = &Ссылка
			|	И нсиКлассификаторНастройкиШаблоновНаименований.Использование <> """"
			|
			|УПОРЯДОЧИТЬ ПО
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки";
		
		Запрос.УстановитьПараметр("Ссылка", Класс);
		
	Иначе 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
	   		"ВЫБРАТЬ
			|	ТекущиеНастройкиФормирования.НомерСтроки,
			|	ТекущиеНастройкиФормирования.ТипЭлемента,
			|	ТекущиеНастройкиФормирования.ЭлементНаименования,
			|	ТекущиеНастройкиФормирования.ПрефиксЗначения,
			|	ТекущиеНастройкиФормирования.ПостфиксЗначения,
			|	ТекущиеНастройкиФормирования.Использование
			|ПОМЕСТИТЬ ТекущиеНастройкиФормирования
			|ИЗ
			|	&ТекущиеНастройкиФормирования КАК ТекущиеНастройкиФормирования
			|
			|;
			|
			|ВЫБРАТЬ
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ТипЭлемента,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ЭлементНаименования,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПрефиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПостфиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.Использование
			|ИЗ
			|	ТекущиеНастройкиФормирования КАК нсиКлассификаторНастройкиШаблоновНаименований
			|ГДЕ
			|	нсиКлассификаторНастройкиШаблоновНаименований.Использование <> """"
			|
			|УПОРЯДОЧИТЬ ПО
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки";
			
		Запрос.УстановитьПараметр("ТекущиеНастройкиФормирования", ТекущиеНастройкиФормирования);
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.ЭлементНаименования) тогда
			Продолжить;
		КонецЕсли;	
		
		ЗначениеВычисления = "";
		
		Если Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.Реквизит тогда
			
			ИмяРеквизита = Выборка.ЭлементНаименования;
			Попытка 
				ЗначениеВычисления = пОбъект[ИмяРеквизита];
			Исключение
				Попытка
					ЗначениеВычисления = Объект[ИмяРеквизита];
				Исключение
				КонецПопытки;
			КонецПопытки;
		ИначеЕсли Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.Характеристика тогда	
			НайденныеСтроки = ТехническиеХарактеристики.НайтиСтроки(Новый Структура("Характеристика", Выборка.ЭлементНаименования));
			Если НайденныеСтроки.ВГраница() > -1 тогда
				ЗначениеВычисления = Строка(НайденныеСтроки[0].Значение);	
			КонецЕсли;	
		ИначеЕсли Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.ПредопределенноеНаименование тогда			
			ЗначениеВычисления = Выборка.ЭлементНаименования;	
		Иначе 
			Продолжить;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ЗначениеВычисления) тогда
			Продолжить;
		КонецЕсли;	
		
		Если 		Выборка.Использование = "ПолноеНаименование" Тогда
			Результат.ПолноеНаименование = Результат.ПолноеНаименование + Выборка.ПрефиксЗначения + ЗначениеВычисления + Выборка.ПостфиксЗначения;	
		ИначеЕсли 	Выборка.Использование = "Наименование" Тогда
			Результат.Наименование = Результат.Наименование + Выборка.ПрефиксЗначения + ЗначениеВычисления + Выборка.ПостфиксЗначения;	
		КонецЕсли;	

	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

// Функция - Получить наименование по шаблону
// Параметры:
// Объект - объект, ссылка или управляемая форма элемента справочника нсиМТР или нсиУслуги.
// 			Если Объект - управляемая форма то функция должна вызываться с сервера.
// Возвращаемое значение:
// Структура  - структура с ключами "Наименование" и "ПолноеНаименование";
//
Функция ПолучитьНаименованиеПоШаблону(Знач ЭтотОбъект, ТекущиеНастройкиФормирования = Неопределено) Экспорт 
	
	ЭтотОбъектУправляемаяФорма = ТипЗнч(ЭтотОбъект) = Тип("УправляемаяФорма");
	
	Если ЭтотОбъектУправляемаяФорма тогда
		Объект = ЭтотОбъект.Объект;
	Иначе 	
		Объект = ЭтотОбъект;
	КонецЕсли;
	
	Результат = Новый Структура("Наименование, ПолноеНаименование","","");
	
	Если ТекущиеНастройкиФормирования = Неопределено Тогда
	
		Класс = Объект.Класс;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ТипЭлемента,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ЭлементНаименования,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПрефиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.ПостфиксЗначения,
			|	нсиКлассификаторНастройкиШаблоновНаименований.Использование
			|ИЗ
			|	Справочник."+Класс.Метаданные().Имя+".НастройкиШаблоновНаименований КАК нсиКлассификаторНастройкиШаблоновНаименований
			|ГДЕ
			|	нсиКлассификаторНастройкиШаблоновНаименований.Ссылка = &Ссылка
			|	И нсиКлассификаторНастройкиШаблоновНаименований.Использование <> """"
			|
			|УПОРЯДОЧИТЬ ПО
			|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки";
		
		Запрос.УстановитьПараметр("Ссылка", Класс);
		
	Иначе 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущиеНастройкиФормирования.НомерСтроки,
		|	ТекущиеНастройкиФормирования.ТипЭлемента,
		|	ТекущиеНастройкиФормирования.ЭлементНаименования,
		|	ТекущиеНастройкиФормирования.ПрефиксЗначения,
		|	ТекущиеНастройкиФормирования.ПостфиксЗначения,
		|	ТекущиеНастройкиФормирования.Использование
		|ПОМЕСТИТЬ ТекущиеНастройкиФормирования
		|ИЗ
		|	&ТекущиеНастройкиФормирования КАК ТекущиеНастройкиФормирования
		|
		|;
		|
		|ВЫБРАТЬ
		|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки,
		|	нсиКлассификаторНастройкиШаблоновНаименований.ТипЭлемента,
		|	нсиКлассификаторНастройкиШаблоновНаименований.ЭлементНаименования,
		|	нсиКлассификаторНастройкиШаблоновНаименований.ПрефиксЗначения,
		|	нсиКлассификаторНастройкиШаблоновНаименований.ПостфиксЗначения,
		|	нсиКлассификаторНастройкиШаблоновНаименований.Использование
		|ИЗ
		|	ТекущиеНастройкиФормирования КАК нсиКлассификаторНастройкиШаблоновНаименований
		|ГДЕ
		|	нсиКлассификаторНастройкиШаблоновНаименований.Использование <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	нсиКлассификаторНастройкиШаблоновНаименований.НомерСтроки";
	
		Запрос.УстановитьПараметр("ТекущиеНастройкиФормирования", ТекущиеНастройкиФормирования);
		
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
		
	Пока Выборка.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(Выборка.ЭлементНаименования) тогда
			Продолжить;
		КонецЕсли;	
		
		ЗначениеВычисления = "";
		
		Если Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.Реквизит тогда
			ИмяРеквизита = Выборка.ЭлементНаименования;
			ЗначениеВычисления = Объект[ИмяРеквизита];	
		ИначеЕсли Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.Характеристика тогда	
			Если ЭтотОбъектУправляемаяФорма тогда
				НайденныеСтроки = ЭтотОбъект.ТехническиеХарактеристикиФорма.НайтиСтроки(Новый Структура("Характеристика", Выборка.ЭлементНаименования));
				Если НайденныеСтроки.ВГраница() > -1 тогда
					ЗначениеВычисления = Строка(НайденныеСтроки[0].Значение);	
				КонецЕсли;	
			Иначе 	
				НайденныеСтроки = Объект.ТехническиеХарактеристики.НайтиСтроки(Новый Структура("Характеристика", Выборка.ЭлементНаименования));
				Если НайденныеСтроки.ВГраница() > -1 тогда
					ЗначениеВычисления = Строка(НайденныеСтроки[0].Значение);	
				КонецЕсли;	
			КонецЕсли;
		ИначеЕсли Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.ДополнительныйРеквизит тогда		
			Если ЭтотОбъектУправляемаяФорма тогда
				НайденныеСтроки = ЭтотОбъект.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("Свойство", Выборка.ЭлементНаименования));
				Если НайденныеСтроки.ВГраница() > -1 тогда
					ЗначениеВычисления = Строка(ЭтотОбъект[НайденныеСтроки[0].ИмяРеквизитаЗначение]);	
				КонецЕсли;	
			Иначе 	
				НайденныеСтроки = Объект.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", Выборка.ЭлементНаименования));
				Если НайденныеСтроки.ВГраница() > -1 тогда
					ЗначениеВычисления = Строка(НайденныеСтроки[0].Значение);	
				КонецЕсли;	
			КонецЕсли;
		ИначеЕсли Выборка.ТипЭлемента = Перечисления.нсиТипыЭлементовНаименования.ПредопределенноеНаименование тогда			
			ЗначениеВычисления = Выборка.ЭлементНаименования;	
		Иначе 
			Продолжить;
		КонецЕсли;	
		
		Если Не ЗначениеЗаполнено(ЗначениеВычисления) тогда
			Продолжить;
		КонецЕсли;	
		
		Если 		Выборка.Использование = "ПолноеНаименование" Тогда
			Результат.ПолноеНаименование = Результат.ПолноеНаименование + Выборка.ПрефиксЗначения + ЗначениеВычисления + Выборка.ПостфиксЗначения;	
		ИначеЕсли 	Выборка.Использование = "Наименование" Тогда
			Результат.Наименование = Результат.Наименование + Выборка.ПрефиксЗначения + ЗначениеВычисления + Выборка.ПостфиксЗначения;	
		КонецЕсли;	

	КонецЦикла;
	
	ПредельнаяДлинаНаименования = Объект.Ссылка.Метаданные().ДлинаНаименования;
	Если СтрДлина(Результат.Наименование) > ПредельнаяДлинаНаименования Тогда
		Результат.Наименование = Лев(Результат.Наименование, ПредельнаяДлинаНаименования);
	КонецЕсли;
	
	ПредельнаяДлинаНаименованияПолного = Объект.Ссылка.Метаданные().Реквизиты.ПолноеНаименование.Тип.КвалификаторыСтроки.Длина;
	Если СтрДлина(Результат.ПолноеНаименование) > ПредельнаяДлинаНаименованияПолного Тогда
		Результат.ПолноеНаименование = Лев(Результат.ПолноеНаименование, ПредельнаяДлинаНаименованияПолного);
	КонецЕсли;
	
	Возврат Результат
	
КонецФункции	

// Процеудра - Установить наименование по шаблону
// Параметры:
// Объект - объект или ссылка на элемент справочника нсиМТР или нсиУслуги.
//
Процедура УстановитьНаименованиеПоШаблону(Объект) Экспорт
	
	Результат = ПолучитьНаименованиеПоШаблону(Объект);
	
	Если ЗначениеЗаполнено(Результат.Наименование) тогда
		Объект.Наименование = Результат.Наименование;	
	КонецЕсли;	
	Если ЗначениеЗаполнено(Результат.ПолноеНаименование) тогда
		Объект.ПолноеНаименование = Результат.ПолноеНаименование;	
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти
