#Область ПрограммныйИнтерфейс

// Функция получает значение константы по имени.
//
// Параметры:
// 	ИмяКонстанты - Строка - имя справочника.
//
// Возвращаемое значение: 
//	значение произвольного типа.
//
Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт
	Возврат Константы[ИмяКонстанты].Получить();
КонецФункции

// Функция выполняет подключение к web-сервису и возвращает параметры с результатом подключения.
//
// Параметры:
//  Адрес               - Строка - месторасположение wsdl.
//  Пользователь        - Строка - имя пользователя для входа на сервер.
//  Пароль              - Строка - пароль пользователя.
//  ПространствоИмен    - Строка - URI пространства имен web-сервиса.
//  ИмяСервиса          - Строка - имя сервиса.
//  ПортСервиса         - Строка - если не задано, образуется как <ИмяСервиса>Soap.
//  ТестовоеПодключение - Булево - признак тестового подключения.
//
// Возвращаемое значение:
//  Структура со свойствами:
//		Результат      - Булево      - Истина - успех, иначе Ложь.
//		ОписаниеОшибки - Строка      - подробное представление ошибки.
//		Прокси         - WSПрокси    - объект WSПрокси созданный с переданными параметрами.
//		Фабрика        - ФабрикаXDTO - объект ФабрикаXDTO созданного WSПрокси.
//
Функция ПодключитьВебСервис( Адрес, Пользователь, Пароль, ПространствоИмен, ИмяСервиса, ПортСервиса, ТестовоеПодключение = Ложь ) Экспорт
	РезультатФункции = Новый Структура("Результат,ОписаниеОшибки,Прокси,Фабрика", Истина, "");
	Попытка
		Определения 				= Новый WSОпределения( Адрес, Пользователь, Пароль);
		Прокси					= Новый WSПрокси( Определения, ПространствоИмен, ИмяСервиса, ПортСервиса);
		Прокси.Пользователь 		= Пользователь;
		Прокси.Пароль       			= Пароль;
		
		Если ТестовоеПодключение = Ложь Тогда
			РезультатФункции.Прокси 	= Прокси;
			РезультатФункции.Фабрика 	= Прокси.ФабрикаXDTO;
		КонецЕсли;
	Исключение
		РезультатФункции.Результат = Ложь;
		РезультатФункции.ОписаниеОшибки = ПодробноеПредставлениеОшибки( ИнформацияОбОшибке() );
	КонецПопытки;
	
	Возврат РезультатФункции;
КонецФункции

#КонецОбласти
