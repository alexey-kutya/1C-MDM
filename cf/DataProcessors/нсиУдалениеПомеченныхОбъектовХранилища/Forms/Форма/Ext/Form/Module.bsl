#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РежимУдаленияПриИзменении(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектыПометкаПриИзменении(Элемент)
	ТекДанные = Элементы.Объекты.ТекущиеДанные;
	СтрокаРодитель = ТекДанные.ПолучитьРодителя();
	Если СтрокаРодитель=Неопределено Тогда
		Для каждого СтрокаЭлемента Из ТекДанные.ПолучитьЭлементы() Цикл
			СтрокаЭлемента.Пометка = ТекДанные.Пометка;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РежимУдаленияПриИзменении(Элемент)
	Если РежимУдаления=0 Тогда
		Элементы.ФормаВыборВарианта.Заголовок = "Удалить >>";
	Иначе
		Элементы.ФормаВыборВарианта.Заголовок = "Найти помеченные >>";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекДанные = Объект.Объекты.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ДанныеТипа = ТекДанные.ПолучитьРодителя();
	Если ДанныеТипа<>Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ТекДанные.ВидСправочника) Тогда
			Если ТипЗнч(ТекДанные.ВидСправочника) = Тип("Строка")
				И Найти(ТекДанные.ВидСправочника,"РегистрСведений.")>0 Тогда 
				ОткрытьФорму(ТекДанные.ВидСправочника+".ФормаЗаписи",новый Структура("Ключ",ПолучитьЗначениеИзСтроки(ТекДанные.Элемент)),ЭтаФорма);
				
			Иначе
				ПоказатьЗначение(,ТекДанные.Элемент);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыборВарианта(Команда)
	ВыборВариантаНаСервере();
	Если РежимУдаления=0 Тогда
		ОповеститьФормы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПометитьВсе(Команда)
	Для каждого СтрокаТипа Из Объект.Объекты.ПолучитьЭлементы() Цикл
		СтрокаТипа.Пометка = Истина;
		Для каждого СтрокаЭлемента Из СтрокаТипа.ПолучитьЭлементы() Цикл
			СтрокаЭлемента.Пометка = Истина;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеПометки(Команда)
	Для каждого СтрокаТипа Из Объект.Объекты.ПолучитьЭлементы() Цикл
		СтрокаТипа.Пометка = Ложь;
		Для каждого СтрокаЭлемента Из СтрокаТипа.ПолучитьЭлементы() Цикл
			СтрокаЭлемента.Пометка = Ложь;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура НайтиПомеченные(Команда)
	НайтиПомеченныеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	УдалитьНаСервере();
	ОповеститьФормы();
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	Объект.Объекты.ПолучитьЭлементы().Очистить();
	Элементы.Страницы.ТекущаяСтраница = Элементы.Начало;
	Элементы.ФормаВыборВарианта.КнопкаПоУмолчанию = Истина;
КонецПроцедуры

&НаКлиенте
Процедура НеудаленныеОбъекты(Команда)
	Элементы.Страницы.ТекущаяСтраница = Элементы.Неудаленные;
	Элементы.ФормаНазад2.КнопкаПоУмолчанию = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВыборВариантаНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Если РежимУдаления=0 Тогда
		Обработка.НайтиПомеченные();
		Обработка.ВыполнитьУдаление();
		Элементы.Страницы.ТекущаяСтраница = Элементы.Результаты;
		
		Если Обработка.Объекты.Строки.Количество()=0 Тогда
			Элементы.ФормаНеудаленныеОбъекты.Видимость = Ложь;
			Элементы.ФормаНазад1.КнопкаПоУмолчанию = Истина;
		Иначе
			Элементы.ФормаНеудаленныеОбъекты.Видимость = Истина;
			Элементы.ФормаНеудаленныеОбъекты.КнопкаПоУмолчанию = Истина;
		КонецЕсли;
	Иначе
		ВсегоОбъектов = Обработка.НайтиПомеченные();
		Элементы.НадписьВсего.Заголовок = "Всего объектов: "+Формат(ВсегоОбъектов, "ЧН=");
		Элементы.Страницы.ТекущаяСтраница = Элементы.ВыборОбъектов;
		Элементы.ФормаУдалить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

&НаСервере
Процедура НайтиПомеченныеНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	ВсегоОбъектов = Обработка.НайтиПомеченные();
	Элементы.НадписьВсего.Заголовок = "Всего объектов: "+Формат(ВсегоОбъектов, "ЧН=");
	ЗначениеВРеквизитФормы(Обработка, "Объект");
КонецПроцедуры

&НаСервере
Процедура УдалитьНаСервере()
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ВыполнитьУдаление();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	Элементы.Страницы.ТекущаяСтраница = Элементы.Результаты;
	Если Обработка.Объекты.Строки.Количество()=0 Тогда
		Элементы.ФормаНеудаленныеОбъекты.Видимость = Ложь;
		Элементы.ФормаНазад1.КнопкаПоУмолчанию = Истина;
	Иначе
		Элементы.ФормаНеудаленныеОбъекты.Видимость = Истина;
		Элементы.ФормаНеудаленныеОбъекты.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьФормы()
	Для каждого СтрокаВида Из Объект.ВидыКОповещению Цикл
		Оповестить("нсиЗаписанЭлементХранилища", СтрокаВида.ВидСправочника);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеИзСтроки(ЗначениеСтрока)
	Возврат ЗначениеИзСтрокиВнутр(ЗначениеСтрока);
КонецФункции

#КонецОбласти
