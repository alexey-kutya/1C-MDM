
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ТекстДляПроверки") Тогда 
		ТекстДляПроверки = Параметры.ТекстДляПроверки;	
	Иначе
		ТекстДляПроверки = "В этом тектсе есть множиство глупих ошыбок";	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолеХТМЛ = ПолучитьПолеХТМЛ();
	ПроверитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНаСервере()

	лпАдресСервера = "speller.yandex.net";
	лпАдресЗапроса = "/services/spellservice.json/checkText";
	лпЗаголовкиЗапроса = Новый Соответствие;
	лпЗаголовкиЗапроса.Вставить("Content-Type", "application/x-www-form-urlencoded");	
	
	//Формируем заголовки и HTTPЗапрос
	лпHTTPЗапрос = Новый HTTPЗапрос(лпАдресЗапроса, лпЗаголовкиЗапроса);
	лпHTTPЗапрос.УстановитьТелоИзСтроки("text="+ТекстДляПроверки + "&options=17", "CESU-8");
	
	лпHTTPСоединение = Новый HTTPСоединение(лпАдресСервера);
	лпHTTPОтвет = лпHTTPСоединение.ОтправитьДляОбработки(лпHTTPЗапрос);
	
	лпРезультатТекст = лпHTTPОтвет.ПолучитьТелоКакСтроку();

	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(лпРезультатТекст);
	
	лпРезультат = ПрочитатьJSON(ЧтениеJSON);
	
	ПолеХТМЛ = СтрЗаменить(ПолеХТМЛ, "&text", ТекстДляПроверки);
	
	Для Каждого стрМас ИЗ лпРезультат Цикл
		
		word 	  = стрМас.word;
		Подсказка = "";
		
		Для Каждого стрПодсказка ИЗ стрМас.s Цикл
			Подсказка = Подсказка + "<p>" + стрПодсказка + "</p>";
		КонецЦикла;
		
		Подсказка = ?(ПустаяСтрока(Подсказка), "Нет подсказок<br>", Подсказка);
		
		Подсказка = Подсказка + "<button>Пропустить</button>";
		
		ПолеХТМЛ = СтрЗаменить(ПолеХТМЛ, word, "<span onclick='hint.call(this,""" + Подсказка + """)' class='error'>" + word + "</span>");

	КонецЦикла;
	
КонецПроцедуры // ПроверитьНаСервере()

&НаСервере
Функция ПолучитьПолеХТМЛ()

	Возврат РеквизитФормыВЗначение("Объект").ПолучитьМакет("html").ПолучитьТекст();	

КонецФункции // ПолучитьПолеХТМЛ()


&НаКлиенте
Процедура Сохранить(Команда)
	
	Элементы.ПолеХТМЛ.document.parentWindow.removeError();	
	
	ПараметрыЗакрытия = Новый Структура("ТекстHTML", Элементы.ПолеХТМЛ.document.Body.innerHTML);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры


&НаКлиенте
Процедура Отменить(Команда)
	
	Закрыть();
	
КонецПроцедуры

