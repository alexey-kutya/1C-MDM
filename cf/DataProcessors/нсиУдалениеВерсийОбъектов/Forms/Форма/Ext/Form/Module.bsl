#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяСправочникаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокЗн = ПолучитьСписокСправочников();
	
	ВыбЭлемент = СписокЗн.ВыбратьЭлемент();
	Если не ВыбЭлемент = Неопределено Тогда
		Объект.ИмяСправочника = ВыбЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоСправочникуПриИзменении(Элемент)
	Если ПоСправочнику Тогда
		Элементы.ИмяСправочника.ТолькоПросмотр = Ложь;
	Иначе
		Элементы.ИмяСправочника.ТолькоПросмотр = Истина;
		Объект.ИмяСправочника = "";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьУдаление(Команда)
	
	Если ПоСправочнику и не ЗначениеЗаполнено(Объект.ИмяСправочника) ТОгда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан справочник!",, "Объект.ИмяСправочника");
		Возврат;
	КонецЕсли;
	
	КолЭлементов = ПолучитьКоличествоУдаляемыхЭлементов();
	
	Если КолЭлементов>0 Тогда 
		Режим = РежимДиалогаВопрос.ДаНет;
		Текст = "ru = 'Операция необратима, будет удалено " + Строка(КолЭлементов) + " элементов! Продолжить выполнение операции?'";
		ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаОтветаВыполнитьУдаление",ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения,НСтр(Текст), Режим, 0);
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Нет элементов для удаления!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаВыполнитьУдаление(Результат,ДП) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполнитьУдалениеСервер();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокСправочников()
	
	ВозвращаемыйСписок = Новый СписокЗначений;
	
	Для Каждого Строка Из Метаданные.Справочники Цикл 
		ВозвращаемыйСписок.Добавить(Строка.Имя, Строка.Синоним);
	КонецЦикла;
	
	Возврат ВозвращаемыйСписок;
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоУдаляемыхЭлементов()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииОбъектов.Объект,
		|	ВерсииОбъектов.НомерВерсии
		|ИЗ
		|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
		|ГДЕ
		|	ВерсииОбъектов.ДатаВерсии >= &ДатаНач
		|	И ВерсииОбъектов.ДатаВерсии <= &ДатаКон";
		
		Если ЗначениеЗаполнено(Объект.ИмяСправочника) Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И ВерсииОбъектов.Объект В
			|			(ВЫБРАТЬ
			|				нсиОбщСправочник.Ссылка
			|			ИЗ
			|				Справочник." + Объект.ИмяСправочника + " КАК нсиОбщСправочник)";
		КонецЕсли;

	
	Запрос.УстановитьПараметр("ДатаНач", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКон", Объект.Период.ДатаОкончания);

	Результат = Запрос.Выполнить().Выбрать();

	Возврат Результат.Количество();
	
КонецФункции

&НаСервере
Процедура ВыполнитьУдалениеСервер()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВерсииОбъектов.Объект,
		|	ВерсииОбъектов.НомерВерсии
		|ИЗ
		|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
		|ГДЕ
		|	ВерсииОбъектов.ДатаВерсии >= &ДатаНач
		|	И ВерсииОбъектов.ДатаВерсии <= &ДатаКон";
		
		Если ЗначениеЗаполнено(Объект.ИмяСправочника) Тогда
			Запрос.Текст = Запрос.Текст + "
			|	И ВерсииОбъектов.Объект В
			|			(ВЫБРАТЬ
			|				нсиОбщСправочник.Ссылка
			|			ИЗ
			|				Справочник." + Объект.ИмяСправочника + " КАК нсиОбщСправочник)";
		КонецЕсли;

	
	Запрос.УстановитьПараметр("ДатаНач", Объект.Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаКон", Объект.Период.ДатаОкончания);

	Результат = Запрос.Выполнить().Выбрать();

	КоличествоУдаляемыхЗаписей = Результат.Количество();
	
	Пока Результат.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Результат.Объект);
		НаборЗаписей.Отбор.НомерВерсии.Установить(Результат.НомерВерсии);
		НаборЗаписей.Записать();
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Удалено " + Строка(КоличествоУдаляемыхЗаписей) + " записей.");

КонецПроцедуры

#КонецОбласти
