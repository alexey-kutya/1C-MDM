#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Предмет) Тогда 
		ЭтаФорма.ТолькоПросмотр = Истина;
		// @Комментарий: Отберем пакетный ввод по предмету.
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	нсиПакетныйВводЭлементовСправочникаПредметы.Ссылка КАК Ссылка
		|ИЗ
		|	БизнесПроцесс.нсиПакетныйВводЭлементовСправочника.Предметы КАК нсиПакетныйВводЭлементовСправочникаПредметы
		|ГДЕ
		|	нсиПакетныйВводЭлементовСправочникаПредметы.Предмет = &Предмет";
		Запрос.УстановитьПараметр("Предмет",	Параметры.Предмет);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			нсиРаботаСФормами.ОтборВСпискеПоЗначению(ЭтаФорма.Список.Отбор.Элементы, "Ссылка", Выборка.Ссылка, ВидСравненияКомпоновкиДанных.Равно);
		Иначе
			нсиРаботаСФормами.ОтборВСпискеПоЗначению(ЭтаФорма.Список.Отбор.Элементы, "Ссылка", Неопределено, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	КонецЕсли;	
	
	// @Комментарий: Скроем кнопку установки пометки на удаление, если прав недостаточно.
	ИмяБизнесПроцесса = "нсиПакетныйВводЭлементовСправочника";
	текКнопкаФормаУстановитьПометкуУдаления = Элементы.Найти("ФормаУстановитьПометкуУдаления");
	Если текКнопкаФормаУстановитьПометкуУдаления <> Неопределено Тогда
		текКнопкаФормаУстановитьПометкуУдаления.Видимость = нсиСравнениеДанныхСервер.ПроверитьПометкуНаУдалениеБизнесПроцессов(БизнесПроцессы[ИмяБизнесПроцесса].ПустаяСсылка());
	КонецЕсли;
	текКнопкаСписокКонтекстноеМенюУстановитьПометкуУдаления = Элементы.Найти("СписокКонтекстноеМенюУстановитьПометкуУдаления");
	Если текКнопкаСписокКонтекстноеМенюУстановитьПометкуУдаления <> Неопределено Тогда
		текКнопкаСписокКонтекстноеМенюУстановитьПометкуУдаления.Видимость = нсиСравнениеДанныхСервер.ПроверитьПометкуНаУдалениеБизнесПроцессов(БизнесПроцессы[ИмяБизнесПроцесса].ПустаяСсылка());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура("Предмет", Параметры.Предмет);
	 ОткрытьФорму("БизнесПроцесс.нсиПакетныйВводЭлементовСправочника.ФормаОбъекта", 
		 Новый Структура("ЗначенияЗаполнения", ПараметрыФормы));
	
КонецПроцедуры

#КонецОбласти

