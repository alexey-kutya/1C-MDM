
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачальныйПризнакВыполнения = Объект.Выполнена;
	ЗадачаОбъект = РеквизитФормыВЗначение("Объект");
	Если Не ЗначениеЗаполнено(ЗадачаОбъект.БизнесПроцесс) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
	ЗаданиеОбъект = ЗадачаОбъект.БизнесПроцесс.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЗаданиеОбъект, "Задание");	
	УстановитьСостояниеЭлементов();
	
	БизнесПроцессыИЗадачиСервер.ФормаЗадачиПриСозданииНаСервере(ЭтаФорма, Объект, 
		Элементы.ГруппаСостояние, Элементы.ДатаИсполнения);
		
	ПредметСтрокой = нсиБизнесПроцессы.ПредметСтрокой(Объект.Предмет);	
	нсиРаботаСФормамиСервер.УстановитьТипПредмета(Объект.Предмет, Элементы.ПризнакЭлемента);
	
	ПредметОригинал = РегистрыСведений.нсиСтатусыОбработкиСправочников.ИзменяемыйОбъект(Объект.Предмет);
	
	УправлениеВидимостьюИДоступом();
	
	нсиРаботаСФормамиСервер.ВывестиСообщениеПриОткрытииФормыЗадачи(ЭтаФорма);
	
//	ITRR Кутья АА локализация	
//	АвторСтрокой = Строка(Объект.Автор) + " ("+Строка(Задание.ГруппаПользователейБП)+")";
	АвторСтрокой = Строка(Объект.Автор);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВыполнитьЗадачу = Ложь;
	Если НЕ (ПараметрыЗаписи.Свойство("ВыполнитьЗадачу", ВыполнитьЗадачу) И ВыполнитьЗадачу) Тогда
		Возврат;
	КонецЕсли;     	
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗадачаОбъект = ТекущийОбъект;
	ЗаданиеОбъект = ЗадачаОбъект.БизнесПроцесс.ПолучитьОбъект();
	ЗаблокироватьДанныеДляРедактирования(ЗаданиеОбъект.Ссылка);
	ЗаданиеОбъект.Выполнено 		= Задание.Выполнено;
	ЗаданиеОбъект.ОтозватьЗаявку 	= Задание.ОтозватьЗаявку;
	ЗаданиеОбъект.ОтклонитьЗаявку 	= Задание.ОтклонитьЗаявку;
	ЗаданиеОбъект.НадоУточнить 		= Задание.НадоУточнить;
	ЗаданиеОбъект.ЕстьОшибки 		= Задание.ЕстьОшибки;
	ЗаданиеОбъект.Записать();
	ЗначениеВРеквизитФормы(ЗаданиеОбъект, "Задание");
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()	
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаданиеВыполнить()
	
	ПоказатьЗначение(,Задание.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура Выполнено(Команда)
	
	Если НайтиВспомогательныеПроцессы(Задание.Ссылка) Тогда 
// ITRR Кутья АА Локализация		
		//ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		//	" Для этого задания есть запросы на доп. информацию.
		//	| обработка информации не может быть закончена.");
		Текст1 = НСтр("ru = 'Для этого задания есть запросы на доп. информацию.'; en = 'For this task there are requests for additional information.'");
		Текст2 = НСтр("ru = 'обработка информации не может быть закончена.'; en = 'information processing can''t be completed.'");
		
		СтрокаСообщения = 
			" %Текст1
			| %Текст2";
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		Возврат;
	КонецЕсли;	
	
	Задание.Выполнено 		= Истина;
	Задание.НадоУточнить 	= Ложь;
	Задание.ОтклонитьЗаявку	= Ложь;
	Задание.ОтозватьЗаявку	= Ложь;
	Задание.ЕстьОшибки 		= Ложь;
	Объект.Обработано 		= Истина;
	Объект.Результат = ПредопределенноеЗначение("Перечисление.нсиРезультатыВыполненияЗадач.Выполнена");
	
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьЗаявку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
// ITRR Кутья АА Локализация		                       
//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указана причина отмены.",,"РезультатВыполнения","Объект");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана причина отмены.'; en = 'The reason for the cancellation is not specified.'"),,"РезультатВыполнения","Объект");
		Возврат;
	КонецЕсли;	
	
	ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаОтветаЗавершитьЗаявку",ЭтаФорма);
// ITRR Кутья АА Локализация		                       
	//ПоказатьВопрос(
	//	ОписаниеОповещения,
	//	"Завершить бизнес-процесс без сохранения изменений в справочнике?",
	//	РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,"Внимание"
	//);
	ПоказатьВопрос(
		ОписаниеОповещения,
		НСтр("ru = 'Завершить бизнес-процесс без сохранения изменений в справочнике?'; en = 'Complete the business process without saving the changes in the directory?'"),
		РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет,НСтр("ru = 'Внимание'; en = 'Attention'")
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаЗавершитьЗаявку(Результат,ДП) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда 
		Задание.Выполнено 		= Ложь;
		Задание.НадоУточнить	= Ложь;
		Задание.ОтозватьЗаявку 	= Ложь;
		Задание.ОтклонитьЗаявку	= Истина;
		Задание.ЕстьОшибки 		= Ложь;
		Объект.Отклонена 		= Истина;
		Объект.Результат = ПредопределенноеЗначение("Перечисление.нсиРезультатыВыполненияЗадач.Отклонена");
		БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТребуетсяУточнение(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.РезультатВыполнения) Тогда 
// ITRR Кутья АА Локализация		                       
//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указано, что необходимо уточнить.",,"РезультатВыполнения","Объект");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указано, что необходимо уточнить.'; en = 'Not Provided, that should be clarified.'"),,"РезультатВыполнения","Объект");
		Возврат;
	КонецЕсли;	
	
	Задание.Выполнено 		= Ложь;
	Задание.НадоУточнить 	= Истина;
	Задание.ОтозватьЗаявку 	= Ложь;
	Задание.ОтклонитьЗаявку	= Ложь;
	Задание.ЕстьОшибки 		= Ложь;
	Объект.Результат = ПредопределенноеЗначение("Перечисление.нсиРезультатыВыполненияЗадач.ЗапрошеноУточнение");
	БизнесПроцессыИЗадачиКлиент.ЗаписатьИЗакрытьВыполнить(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьМакет(Команда)
	Если ЗначениеЗаполнено(Объект.Предмет) И нсиОбщегоНазначения.ПолучитьЗначениеРеквизита(Объект.Предмет,"ЭтоМакет") Тогда 
		ПоказатьПредупреждение(,"Макет уже создан!");
	Иначе
		Отказ = Ложь;
		СоздатьМакетНаСервере(Отказ);
		Если Отказ Тогда 
			ПоказатьПредупреждение(,"Не удалось создать макет!");
		Иначе
			ПоказатьОповещениеПользователя("Макет создан!");
		КонецЕсли;
		УправлениеВидимостьюИДоступом();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредметНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяФормыПредмета = нсиБизнесПроцессыРаботаСоСправочниками.ПолучитьИмяФормыОбъекта(Задание.ИмяСправочника, Объект.Предмет);
	ПараметрыФормы = Новый Структура("Ключ,ВременныйЭлемент", Объект.Предмет, Истина);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредметНажатиеЗавершение", ЭтотОбъект);
	
	ОткрытьФорму(ИмяФормыПредмета, ПараметрыФормы, Элемент,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметНажатиеЗавершение(Результат,ДП) Экспорт
	ПредметСтрокой = нсиБизнесПроцессы.ПредметСтрокой(Объект.Предмет);
КонецПроцедуры

&НаКлиенте
Процедура АвторНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(,Объект.Автор);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеВидимостьюИДоступом()
	ТолькоПросмотр = ТолькоПросмотр ИЛИ Объект.Выполнена ИЛИ Объект.Исполнитель<>ПараметрыСеанса.ТекущийПользователь;
	
	Элементы.Выполнено.Доступность = Не ЭтаФорма.ТолькоПросмотр;
	Элементы.СоздатьМакет.Доступность = НЕ ЭтаФорма.ТолькоПросмотр И НЕ Объект.Предмет.ЭтоМакет;
	Элементы.ТребуетсяУточнение.Доступность = Не ЭтаФорма.ТолькоПросмотр;
	Элементы.ОтклонитьЗаявку.Доступность = Не ЭтаФорма.ТолькоПросмотр;
	Элементы.ОписаниеРезультата.ТолькоПросмотр = ЭтаФорма.ТолькоПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеЭлементов()
	
	БизнесПроцессы.Задание.УстановитьСостояниеЭлементовФормыЗадачи(ЭтаФорма);
	
КонецПроцедуры	

&НаСервереБезКонтекста
Функция НайтиВспомогательныеПроцессы(БП)

	Возврат нсиБизнесПроцессы.НайтиВспомогательныеПроцессы(БП);
		
КонецФункции	

&НаСервере
Процедура СоздатьМакетНаСервере(Отказ)
	УстановитьПривилегированныйРежим(Истина);
	ЗаданиеОбъект = РеквизитФормыВЗначение("Задание");
	ЗаданиеОбъект.СоздатьВременныйЭлемент(Отказ);
	ЗначениеВРеквизитФормы(ЗаданиеОбъект,"Задание");
	
	Объект.Предмет = ЗаданиеОбъект.Предмет;
	Отказ = Отказ ИЛИ НЕ Записать();
	
	Если Отказ Тогда 
		Объект.Предмет = Объект.Ссылка.Предмет;
	КонецЕсли;
	
	ПредметСтрокой = нсиБизнесПроцессы.ПредметСтрокой(Объект.Предмет);
	Если Объект.Предмет.ЭтоМакет Тогда 
		ПредметСтрокой = ПредметСтрокой + "(макет)";
	КонецЕсли;
	нсиРаботаСФормамиСервер.УстановитьТипПредмета(Объект.Предмет, Элементы.ПризнакЭлемента);
	УстановитьПривилегированныйРежим(Ложь);
	//--
	
КонецПроцедуры

#КонецОбласти
