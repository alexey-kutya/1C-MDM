#Область ПрограммныйИнтерфейс

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения<>Неопределено Тогда 
		Тип = ТипЗнч(ДанныеЗаполнения);
		Если Справочники.ТипВсеСсылки().СодержитТип(Тип) Тогда  
			Если Тип = Тип("СправочникСсылка.нсиУниверсальныйФункциональныйСправочник")
				ИЛИ Тип = Тип("СправочникСсылка.нсиУниверсальныйКлассификатор") Тогда 
				ИмяСправочника = ДанныеЗаполнения.Владелец;
			Иначе
				ИмяСправочника = ДанныеЗаполнения.Метаданные().Имя;
			КонецЕсли;
			Предмет = ДанныеЗаполнения;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	СтандартнаяОбработка = Ложь;
	Номер = (Формат(нсиБизнесПроцессы.ПолучитьНовыйНомер("НумераторБП"),"ЧЦ=9; ЧВН=; ЧГ=0"));
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА УСЛОВИЙ

Процедура ОтозватьЗаявкуПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = ОтозватьЗаявку;
КонецПроцедуры

Процедура ЕстьОшибкиПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = ЕстьОшибки;
	Если ЕстьОшибки Тогда 
		ОтменитьПрохождение(Перечисления.нсиШагиБП.Обработка);
		ОтменитьПрохождение(Перечисления.нсиШагиБП.Утверждение);
	КонецЕсли;
КонецПроцедуры

Процедура УсловиеОтклонитьЗаявкуПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = ОтклонитьЗаявку;
КонецПроцедуры

Процедура НадоУточнитьПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = НадоУточнить;
КонецПроцедуры

Процедура УсловиеЕстьЭтапыДиспетчеризацииПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	СтрокиЭтапов = ПрохождениеЭтапов.НайтиСтроки(
		новый Структура("Шаг,Пройден",
			Перечисления.нсиШагиБП.РаспределениеЗадач,
			Ложь
		)
	);
	
	Результат = Ложь;
	Для каждого Строка Из СтрокиЭтапов Цикл
		Если нсиБизнесПроцессы.УсловиеВыполняется(Строка.УсловиеВыполнения,Ссылка) Тогда 
			Результат = Истина;
			Прервать;
		Иначе
			Строка.Пройден = Истина;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УсловиеЕстьЭтапыОбработкиПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	СтрокиЭтапов = ПрохождениеЭтапов.НайтиСтроки(
		новый Структура("Шаг,Пройден",
			Перечисления.нсиШагиБП.Обработка,
			Ложь
		)
	);
	
	Результат = Ложь;
	Для каждого Строка Из СтрокиЭтапов Цикл
		Если нсиБизнесПроцессы.УсловиеВыполняется(Строка.УсловиеВыполнения,Ссылка) Тогда 
			Результат = Истина;
			Прервать;
		Иначе
			Строка.Пройден = Истина;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

Процедура УсловиеЕстьЭтапыУтвержденияПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	СтрокиЭтапов = ПрохождениеЭтапов.НайтиСтроки(
		новый Структура("Шаг,Пройден",
			Перечисления.нсиШагиБП.Утверждение,
			Ложь
		)
	);
	
	Результат = Ложь;
	Для каждого Строка Из СтрокиЭтапов Цикл
		Если нсиБизнесПроцессы.УсловиеВыполняется(Строка.УсловиеВыполнения,Ссылка) Тогда 
			Результат = Истина;
			Прервать;
		Иначе
			Строка.Пройден = Истина;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВЫПОЛНЕНИЕ ОБРАБОТОК

// Процедура - создает макет элемента справочника.
// Тип макета определяется по имени справочника
Процедура СоздатьМакетЭлементаСправочникаОбработка(ТочкаМаршрутаБизнесПроцесса) Экспорт
	Отказ = Ложь;
	СоздатьВременныйЭлемент(Отказ);
	Если Отказ Тогда 
		ВызватьИсключение "Временный элемент не создан!";
	КонецЕсли;
КонецПроцедуры

Процедура УдалитьМакетЭлементаСправочникаОбработка(ТочкаМаршрутаБизнесПроцесса)
	
	КлючеваяОперация_ЗавершениеЗаявки = "Завершение заявки на изменение элемента справочника """+Строка(ИмяСправочника)+"""";
	ВремяНачала_ЗавершениеЗаявки = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация_ЗавершениеЗаявки);
	ДополнительныеСвойства.Вставить("ВремяНачала_ЗавершениеЗаявки",ВремяНачала_ЗавершениеЗаявки);
	ДополнительныеСвойства.Вставить("КлючеваяОперация_ЗавершениеЗаявки",КлючеваяОперация_ЗавершениеЗаявки);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(Предмет) Тогда 
		Возврат;
	КонецЕсли;	

	Если НЕ Предмет.ЭтоМакет Тогда 
		РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(Предмет,
			Новый Структура("Пользователь,СозданаЗаявка", Неопределено, Ложь));
		Возврат;
	КонецЕсли;
	
	ОбъектМакета = Предмет.ПолучитьОбъект();
	ОбъектМакета.УстановитьПометкуУдаления(Истина);
	
	РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(Предмет,
		Новый Структура("Пользователь,ВременныйЭлемент,СозданаЗаявка", Неопределено, Истина,Ложь));
		
	РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(ПредметОригинал,
		Новый Структура("Пользователь,СозданаЗаявка", Неопределено, Ложь));
				
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ИзменитьЭлементСправочникаОбработка(ТочкаМаршрутаБизнесПроцесса)

	КлючеваяОперация_ИзменениеЭлемента = "Изменение элемента справочника по заявке ("""+Строка(ИмяСправочника)+""")";
	ВремяНачала_ИзменениеЭлемента = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация_ИзменениеЭлемента);
	
	КлючеваяОперация_ЗавершениеЗаявки = "Завершение заявки на изменение элемента справочника """+Строка(ИмяСправочника)+"""";
	ВремяНачала_ЗавершениеЗаявки = ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени(КлючеваяОперация_ЗавершениеЗаявки);
	ДополнительныеСвойства.Вставить("ВремяНачала_ЗавершениеЗаявки",ВремяНачала_ЗавершениеЗаявки);
	ДополнительныеСвойства.Вставить("КлючеваяОперация_ЗавершениеЗаявки",КлючеваяОперация_ЗавершениеЗаявки);
	
	УстановитьПривилегированныйРежим(Истина);
	

	РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(ПредметОригинал,
		Новый Структура("Пользователь,СозданаЗаявка", Неопределено, Ложь));
				
	Если ЗначениеЗаполнено(ПредметОригинал) И ПредметОригинал <> Предмет Тогда 
		ОбъектОригинал = ПредметОригинал.ПолучитьОбъект();
		ОбъектОригинал.Заполнить(Предмет);
		
		// Для классификаторов обновляем характеристики.
		Если ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиКлассификаторПродукции")
			ИЛИ ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиКлассификаторУслуг")
			ИЛИ ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиУниверсальныйКлассификатор") тогда
			ОбъектОригинал.ОбновитьХарактеристикиПоМакету(Предмет);
		КонецЕсли;	
		
		//Кутья АА
		
		//ОбъектОригинал.ОбменДанными.Загрузка = Истина;

		ОбъектОригинал.Записать();
	КонецЕсли;	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(КлючеваяОперация_ИзменениеЭлемента,ВремяНачала_ИзменениеЭлемента);	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ЗАДАЧ

Процедура ЗадачаПриВыполнении(ТочкаМаршрутаБизнесПроцесса, Задача, Отказ)

	РезультатВыполнения = РезультатВыполненияДляТочек(Задача, ТочкаМаршрутаБизнесПроцесса.Имя) + РезультатВыполнения;
	
	Если Задача.Результат <> Перечисления.нсиРезультатыВыполненияЗадач.ЗапрошеноУточнение 
		И НЕ (
			ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.НазначениеОтветственного 
			И Задача.Результат = Перечисления.нсиРезультатыВыполненияЗадач.Отклонена
		)
		Тогда 
		
		СтрокаЭтапа = ПрохождениеЭтапов.НайтиСтроки(новый Структура("Шаг,НомерЭтапа",Задача.ШагБП,Задача.НомерЭтапаБП));
		Если СтрокаЭтапа.Количество()>0 Тогда 
			СтрокаЭтапа[0].Пройден = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗадачаОбъект = Задача.ПолучитьОбъект();
	ЗадачаОбъект.ДатаИсполнения = ТекущаяДата();
	
	ЗадачаОбъект.ДлительностьИсполнения 	= 
		нсиБизнесПроцессы.ОпределитьДлительностьПоГрафику(
			Задача.ДатаНачала,
			Задача.ДатаИсполнения,
			Задача.Исполнитель
	);
	ЗадачаОбъект.Записать();
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	
КонецПроцедуры

Функция ПолучитьШагЭтапИсполнителя(ТочкаМаршрутаБизнесПроцесса)
	Результат = Новый Структура(
		"Шаг,
		|НомерЭтапа,
		|СпособРаспределения,
		|РольИсполнителя,
		|Исполнитель,
		|ОсновнойОбъектАдресации,
		|ВремяИсполнения,
		|ВремяОповещения"
	);
	
	Если ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.НазначениеОтветственного Тогда 
		Результат.Шаг = Перечисления.нсиШагиБП.РаспределениеЗадач;
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.ОбработкаИнформации Тогда 
		Результат.Шаг = Перечисления.нсиШагиБП.Обработка;
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.КонтрольИсполнения Тогда 
		Результат.Шаг = Перечисления.нсиШагиБП.Утверждение;
	ИначеЕсли ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.УточнениеИнформации Тогда 
		Результат.Шаг = Перечисления.нсиШагиБП.ПустаяСсылка();
		Результат.НомерЭтапа = 0;
		Результат.СпособРаспределения = Перечисления.нсиСпособыРаспределенияЗадач.ПустаяСсылка();
		Результат.РольИсполнителя = Справочники.РолиИсполнителей.ПустаяСсылка();
		Результат.Исполнитель = Автор;
		Результат.ОсновнойОбъектАдресации = Неопределено;
		Результат.ВремяИсполнения = 0;
		Результат.ВремяОповещения = 0;
		Возврат Результат;
	КонецЕсли;
	
	
	нсиБизнесПроцессы.ЗаполнитьПараметрыЭтапа(ЭтотОбъект,Результат);
	
	Возврат Результат;
		
КонецФункции

Процедура ЗаполнениеПриСозданииЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, Отказ)
	
	Для Каждого Задача Из ФормируемыеЗадачи Цикл
		
		Задача.Автор 			= Автор;
		Задача.Наименование 	= ""+ТочкаМаршрутаБизнесПроцесса+": "+Наименование;
		Задача.Предмет 			= Предмет;
		Задача.ДатаНачала = ТекущаяДата();
		
		Если ТочкаМаршрутаБизнесПроцесса = БизнесПроцессы.нсиИзменениеЭлементаСправочника.ТочкиМаршрута.УточнениеИнформации Тогда 
			Задача.Исполнитель 		= Автор;
			Исполнитель = Автор;
			Задача.РольИсполнителя  = Автор;
			Задача.СпособРаспределения = Перечисления.нсиСпособыРаспределенияЗадач.ПустаяСсылка();
		Иначе
			Структура = ПолучитьШагЭтапИсполнителя(ТочкаМаршрутаБизнесПроцесса);
			Задача.Исполнитель 		= Структура.Исполнитель;
			Исполнитель = Структура.Исполнитель;
			Задача.РольИсполнителя  = Структура.РольИсполнителя;
			Задача.ШагБП            = Структура.Шаг;
			Задача.НомерЭтапаБП     = Структура.НомерЭтапа;
			Задача.ОсновнойОбъектАдресации = Структура.ОсновнойОбъектАдресации;
			Задача.СпособРаспределения = Структура.СпособРаспределения;
			Задача.СрокИсполнения 	= 
				нсиБизнесПроцессы.ОпределитьДатуОкончанияПоКалендарномуГрафику(
					Задача.ДатаНачала,
					Структура.ВремяИсполнения,
					Структура.Исполнитель
			);
			Задача.СрокОповещения 	= 
				нсиБизнесПроцессы.ОпределитьДатуОкончанияПоКалендарномуГрафику(
					Задача.ДатаНачала,
					Структура.ВремяОповещения,
					Структура.Исполнитель
			);
		КонецЕсли;
		
		
		Если Не ПредметОригинал = Предмет Тогда 
			РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(ПредметОригинал,
				Новый Структура("Пользователь,СозданаЗаявка", Исполнитель, Истина));
			РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(Предмет,
				Новый Структура("Пользователь,ВременныйЭлемент", Исполнитель, Истина));
		Иначе 		
			РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(Предмет,
				Новый Структура("Пользователь,СозданаЗаявка", Исполнитель, Истина));
		КонецЕсли;	
		
		
		нсиБизнесПроцессы.ОтправитьОповещениеПоЭлектроннойПочте(Задача);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	Записать();
	
КонецПроцедуры

// Функция - возвращает результат выполнения для точки по шаблону.
//
Функция РезультатВыполненияДляТочек(Знач ЗадачаСсылка, ПеренаправитьЗадачи = Неопределено) Экспорт
	
#Если Сервер ИЛИ ВнешнееСоединение тогда	
	
//ITRR Кутья АА локализация
	СтрокаВставки = НСтр("ru = 'обработал(а) задачу:'; en = 'processed the task:'");
	Если ПеренаправитьЗадачи = "НазначениеОтветственного" Тогда
		Если не ОтклонитьЗаявку Тогда
			СтрокаВставки = НСтр("ru = 'исполнитель назначен:'; en = 'the executor is appointed:'");
		Иначе
			СтрокаВставки = НСтр("ru = 'отклонено:'; en = 'rejected:'");
		КонецЕсли;
	ИначеЕсли ЗадачаСсылка.Результат = Перечисления.нсиРезультатыВыполненияЗадач.Перенаправлена Тогда
		Возврат "";
	ИначеЕсли ЗадачаСсылка.Результат = Перечисления.нсиРезультатыВыполненияЗадач.ВзятаВОбработку Тогда
		Возврат "";
	ИначеЕсли ПеренаправитьЗадачи = "ОбработкаИнформации" Тогда
		Если Выполнено Тогда
			СтрокаВставки = НСтр("ru = 'обработано:'; en = 'processed:'");
		ИначеЕсли НадоУточнить Тогда
			СтрокаВставки = НСтр("ru = 'отправлено на уточнение:'; en = 'sent to clarify:'");
		ИначеЕсли ОтклонитьЗаявку Тогда
			СтрокаВставки = НСтр("ru = 'отклонено:'; en = 'rejected:'");
		КонецЕсли;
	ИначеЕсли ПеренаправитьЗадачи = "УточнениеИнформации" Тогда
		Если Выполнено Тогда
			СтрокаВставки = НСтр("ru = 'уточнено:'; en = 'refined:'");
		ИначеЕсли ОтозватьЗаявку Тогда
			СтрокаВставки = НСтр("ru = 'отозвано:'; en = 'withdrawn:'");
		КонецЕсли;
	ИначеЕсли ПеренаправитьЗадачи = "КонтрольИсполнения" Тогда
		Если ЕстьОшибки Тогда
			СтрокаВставки = НСтр("ru = 'отправлено на доработку:'; en = 'sent for revision:'");
		ИначеЕсли Выполнено Тогда
			СтрокаВставки = НСтр("ru = 'заявка выполнена:'; en = 'request completed:'");
		ИначеЕсли ОтклонитьЗаявку Тогда
			СтрокаВставки = НСтр("ru = 'отклонено:'; en = 'rejected:'");
		КонецЕсли;
	КонецЕсли;
	
	//СтрокаФормат = НСтр("ru = '%1, %2, " + СтрокаВставки + "
	//	|%3
	//	|'");
	СтрокаФормат = "%1, %2, " + СтрокаВставки + "
		|%3
		|";
	//--
	ЗадачаДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗадачаСсылка, 
		"РезультатВыполнения,ДатаИсполнения,Исполнитель");
	Комментарий = СокрЛП(ЗадачаДанные.РезультатВыполнения);
	Комментарий = ?(ПустаяСтрока(Комментарий), "", Комментарий + Символы.ПС);
	Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаФормат, 
	              ЗадачаДанные.ДатаИсполнения,
	              ЗадачаДанные.Исполнитель,
	              Комментарий);
	
	Возврат Результат;
	
#Иначе
	
	Возврат "";
	
#КонецЕсли
	
КонецФункции

// Процедура - заполняет табличную часть "Прохождение этапов"
// Заполнение осуществляется по настройке бизнес-процесса
Процедура ЗаполнитьПрохождениеЭтапов() Экспорт
	ПрохождениеЭтапов.Очистить();
	Если НЕ ЗначениеЗаполнено(НастройкаБП) Тогда 
		Возврат;
	КонецЕсли;
	
	Для Каждого Шаг Из НастройкаБП.ШагиБП Цикл 
		Этапы = НастройкаБП.ЭтапыБП.НайтиСтроки(новый Структура("Шаг",Шаг.Шаг));
		Для Каждого Этап ИЗ Этапы Цикл 
			Если НЕ ЗначениеЗаполнено(Этап.УсловиеВыполненияТипПредмета)
				ИЛИ Этап.УсловиеВыполненияТипПредмета = ИмяСправочника Тогда 
				
				НС = ПрохождениеЭтапов.Добавить();
				ЗаполнитьЗначенияСвойств(НС,Этап);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Процедура ЗавершениеПриЗавершении(ТочкаМаршрутаБизнесПроцесса, Отказ)
	ДатаЗавершения = ТекущаяДата();
	нсиБизнесПроцессы.ОтправитьОповещениеОЗавершенииБП(Ссылка);
	Исполнитель = Справочники.Пользователи.ПустаяСсылка();
	
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(Предмет,
		Новый Структура("Пользователь,СозданаЗаявка", Неопределено, Ложь));
		
	Если ЗначениеЗаполнено(ПредметОригинал) Тогда 
		РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(ПредметОригинал,
			Новый Структура("Пользователь,СозданаЗаявка", Неопределено, Ложь));
	КонецЕсли;
	
	Записать();
	
	ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремени(
		ДополнительныеСвойства.КлючеваяОперация_ЗавершениеЗаявки,
		ДополнительныеСвойства.ВремяНачала_ЗавершениеЗаявки
	);	
	
КонецПроцедуры

Процедура ОтменитьПрохождение(Шаг)
	СтрокиЭтапов = ПрохождениеЭтапов.НайтиСтроки(новый Структура("Шаг",Шаг));
	Для Каждого Строка Из СтрокиЭтапов Цикл 
		Строка.Пройден = Ложь;
	КонецЦикла;
КонецПроцедуры

Процедура СоздатьВременныйЭлемент(Отказ) Экспорт
	
	Если Предмет = Неопределено тогда
		Возврат;
	КонецЕсли;	
	
	Если Предмет.ЭтоМакет Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	врПредмет = Предмет;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("БизнесПроцесс.нсиИзменениеЭлементаСправочника");
		ЭлементБлокировки.УстановитьЗначение("Ссылка",Ссылка); 
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Если нсиБизнесПроцессыРаботаСоСправочниками.ЭтоИерархияГруппИЭлементов(ИмяСправочника) И Предмет.ЭтоГруппа Тогда
			ОбъектМакета = нсиБизнесПроцессыРаботаСоСправочниками.СоздатьГруппуСправочника(ИмяСправочника);
		Иначе
			ОбъектМакета = нсиБизнесПроцессыРаботаСоСправочниками.СоздатьЭлементСправочника(ИмяСправочника);
		КонецЕсли;
		СсылкаНового = нсиБизнесПроцессыРаботаСоСправочниками.ПолучитьНовуюСсылку(ИмяСправочника);
		ОбъектМакета.УстановитьСсылкуНового(СсылкаНового);
		ОбъектМакета.Заполнить(Предмет);
		Если НЕ Стартован Тогда 
			РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(СсылкаНового,
			Новый Структура("Пользователь,ВременныйЭлемент,СозданаЗаявка,ИзменяемыйОбъект",Автор,Истина,Истина,Предмет));
		Иначе
			РегистрыСведений.нсиСтатусыОбработкиСправочников.УстановитьСтатусСправочника(СсылкаНового,
			Новый Структура("Пользователь,ВременныйЭлемент,СозданаЗаявка,ИзменяемыйОбъект",Исполнитель,Истина,Истина,Предмет));
		КонецЕсли;
		
		// Для классификаторов копируем характеристики.
		Если ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиКлассификаторПродукции")
			ИЛИ ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиКлассификаторУслуг")
			ИЛИ ТипЗнч(Предмет) = Тип("СправочникСсылка.нсиУниверсальныйКлассификатор") тогда
			ОбъектМакета.СкопироватьХарактеристикиОригинала(Предмет);
		КонецЕсли;	
		
		ОбъектМакета.ДополнительныеСвойства.Вставить("СозданиеИзБП", Истина);
		ОбъектМакета.ЭтоМакет = Истина;
		// Кутья АА
		//ОбъектМакета.ОбменДанными.Загрузка = Истина;
		ОбъектМакета.Записать();
		ПредметОригинал = Предмет;
		Предмет = ОбъектМакета.Ссылка;
			
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		Предмет = врПредмет;
		ПредметОригинал = Неопределено;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			"Не удалось создать временный элемент: "+ОписаниеОшибки(),,,,
			Отказ
		);
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры

#КонецОбласти