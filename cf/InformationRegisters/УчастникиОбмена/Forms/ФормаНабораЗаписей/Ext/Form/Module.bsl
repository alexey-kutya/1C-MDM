
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ключ = Параметры.Ключ;
	
	ЗаполнитьНаборЗаписей();
	
	УстановитьУсловноеОформление();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаборЗаписей()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обмен_МДМ_УПП.Ссылка КАК УзелОбмена,
		|	ЕСТЬNULL(УчастникиОбмена.ОбъектОбмена, &ОбъектОбмена) КАК ОбъектОбмена,
		|	ЕСТЬNULL(УчастникиОбмена.Обмен, ЛОЖЬ) КАК Обмен,
		|	ЕСТЬNULL(УчастникиОбменаВладелец.Обмен, ЛОЖЬ) КАК ОбменВладелец
		|ИЗ
		|	ПланОбмена.Обмен_МДМ_УПП КАК Обмен_МДМ_УПП
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиОбмена КАК УчастникиОбмена
		|		ПО (УчастникиОбмена.УзелОбмена = Обмен_МДМ_УПП.Ссылка)
		|			И (УчастникиОбмена.ОбъектОбмена = &ОбъектОбмена)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчастникиОбмена КАК УчастникиОбменаВладелец
		|		ПО (&ДобавитьОбменВладельца)
		|			И (УчастникиОбменаВладелец.УзелОбмена = Обмен_МДМ_УПП.Ссылка)
		|			И (УчастникиОбменаВладелец.ОбъектОбмена = &Владелец)
		|ГДЕ
		|	Обмен_МДМ_УПП.ЭтотУзел = ЛОЖЬ
		|	И Обмен_МДМ_УПП.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ОбъектОбмена", Ключ);
	Если ТипЗнч(Ключ) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Запрос.УстановитьПараметр("Владелец", Ключ.Владелец);
		Запрос.УстановитьПараметр("ДобавитьОбменВладельца", Истина);
	ИначеЕсли ТипЗнч(Ключ) = Тип("СправочникСсылка.СпецификацииНоменклатуры") Тогда
		Запрос.УстановитьПараметр("Владелец", Ключ.Номенклатура);
		Запрос.УстановитьПараметр("ДобавитьОбменВладельца", Истина);
	Иначе
		Запрос.УстановитьПараметр("Владелец", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
		Запрос.УстановитьПараметр("ДобавитьОбменВладельца", Ложь);
	КонецЕсли; 
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	НаборЗаписей.Сортировать("УзелОбмена");

КонецПроцедуры // ЗаполнитьНаборЗаписейНаСервере()

&НаСервере
Процедура УстановитьУсловноеОформление()

	Элементы.ОбменВладелец.Видимость = Ложь;
	
	ОбъектОбмена = Ключ;
	Если ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		ИЛИ ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.СпецификацииНоменклатуры") Тогда
		
		УзелSAP = ПланыОбмена.Обмен_МДМ_УПП.УзелSAP();
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();            
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НаборЗаписей.Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.УзелОбмена");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = УзелSAP;
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(128, 128, 128));
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();            
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НаборЗаписей.Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.ОбменВладелец");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(128, 128, 128));
		
		Элементы.ОбменВладелец.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриИзменении(Элемент)
	
	Если Элемент.ТекущийЭлемент.Имя = "Обмен" Тогда
	
		ТекущиеДанные = Элемент.ТекущиеДанные;
		НоваяЗаписьНаСервере(Новый Структура("ОбъектОбмена, УзелОбмена, Обмен", ТекущиеДанные.ОбъектОбмена, ТекущиеДанные.УзелОбмена, ТекущиеДанные.Обмен));
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура НоваяЗаписьНаСервере(СтруктураЗаписи)

	ОбъектОбмена = СтруктураЗаписи.ОбъектОбмена;
	Обмен = СтруктураЗаписи.Обмен;
	УзелОбмена = СтруктураЗаписи.УзелОбмена;
	
	ЗаписатьУчастникаОбмена(ОбъектОбмена, УзелОбмена, Обмен);
	
	ПланыОбмена.Обмен_MDM_AX.ВыполнитьРегистрациюДляAX(ОбъектОбмена, Обмен);
	
	УзлыОбменаМассив = Новый Массив;
	УзлыОбменаМассив.Добавить(УзелОбмена);
	
	ПланыОбмена.Обмен_МДМ_УПП.ВыполнитьРегистрациюДляУПП(ОбъектОбмена, УзлыОбменаМассив, Обмен, Истина, Истина);
	
//	УдалитьСправочникиВладельцаИзУчастниковОбмена(ОбъектОбмена, УзелОбмена, Обмен);
	
КонецПроцедуры // НоваяЗаписьНаСервере()

&НаСервереБезКонтекста
Процедура УдалитьСпецификацииИзОбмена(УзелОбмена, Номенклатура)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СпецификацииНоменклатуры.Ссылка
		|ИЗ
		|	Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
		|ГДЕ
		|	СпецификацииНоменклатуры.Номенклатура = &Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ЗаписатьУчастникаОбмена(ВыборкаДетальныеЗаписи.Ссылка, УзелОбмена, Ложь);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьДоговорыИзОбмена(УзелОбмена, Владелец)

	Выборка = Справочники.ДоговорыКонтрагентов.Выбрать(,Владелец);
	Пока Выборка.Следующий() Цикл
		ЗаписатьУчастникаОбмена(Выборка.Ссылка, УзелОбмена, Ложь);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьУчастникаОбмена(ОбъектОбмена, УзелОбмена, Обмен)
	
	МенеджерЗаписи = РегистрыСведений.УчастникиОбмена.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОбъектОбмена = ОбъектОбмена;
	МенеджерЗаписи.УзелОбмена = УзелОбмена;
	МенеджерЗаписи.Обмен = Обмен;
	МенеджерЗаписи.Записать(Истина);
	
КонецПроцедуры // ЗаписатьУчастникаОбмена(ОбъектОбмена, УзелОбмена, Обмен)()

&НаСервереБезКонтекста
Процедура УдалитьСправочникиВладельцаИзУчастниковОбмена(ОбъектОбмена, УзелОбмена, Обмен)

	Если ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.нсиМТР") Тогда
		
		Если НЕ Обмен Тогда
			УдалитьСпецификацииИзОбмена(УзелОбмена, ОбъектОбмена);
		КонецЕсли; 
		
	ИначеЕсли ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.нсиКонтрагенты")
		ИЛИ ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.Организации") 
		Тогда	
		
		Если НЕ Обмен 
			И ТипЗнч(ОбъектОбмена) = Тип("СправочникСсылка.нсиКонтрагенты") Тогда
			УдалитьДоговорыИзОбмена(УзелОбмена, ОбъектОбмена);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // УдалитьСправочникиВладельцаИзУчастниковОбмена()
 
&НаСервере
Процедура ОбновитьНаСервере()
	
	ЗаполнитьНаборЗаписей();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры
