#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ТолькоПросмотр = Истина;
	
	ОбновитьКартуМаршрута();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КартаМаршрутаВыбор(Элемент)
	ОткрытьСписокЗадачТочкиМаршрута();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьКартуМаршрута()
	
	Если Не Константы.ИспользоватьБизнесПроцессыИЗадачи.Получить() Тогда 
		Возврат;
	КонецЕсли;	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ЗадачаИсполнителя.БизнесПроцесс,
		|	ЗадачаИсполнителя.ТочкаМаршрута
		|ИЗ
		|	Задача.ЗадачаИсполнителя КАК ЗадачаИсполнителя
		|ГДЕ
		|	(ЗадачаИсполнителя.БизнесПроцесс.Предмет = &Предмет
		|			ИЛИ ЗадачаИсполнителя.БизнесПроцесс.ПредметОригинал = &Предмет)
		|	И (НЕ ЗадачаИсполнителя.БизнесПроцесс.Завершен)
		|	И (НЕ ЗадачаИсполнителя.БизнесПроцесс.ПометкаУдаления)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗадачаИсполнителя.Дата УБЫВ,
		|	ЗадачаИсполнителя.Номер УБЫВ";

	Запрос.УстановитьПараметр("Предмет", Запись.Объект);

	Результат = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = Результат.Выбрать();

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		БизнесПроцесс = ВыборкаДетальныеЗаписи.БизнесПроцесс;
		ТочкаМаршрута = ВыборкаДетальныеЗаписи.ТочкаМаршрута;
	КонецЦикла;	
	
	Если ЗначениеЗаполнено(БизнесПроцесс) Тогда
		КартаМаршрута = БизнесПроцесс.ПолучитьОбъект().ПолучитьКартуМаршрута();
	ИначеЕсли БизнесПроцесс <> Неопределено Тогда
		КартаМаршрута = БизнесПроцессы[БизнесПроцесс.Метаданные().Имя].ПолучитьКартуМаршрута();
	Иначе
		КартаМаршрута = Новый ГрафическаяСхема;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокЗадачТочкиМаршрута()

#Если ВебКлиент Тогда
	ПоказатьПредупреждение(,"В Веб-клиенте данная операция недоступна.");
	Возврат;
#КонецЕсли
	ОчиститьСообщения();
	ТекЭлемент = Элементы.КартаМаршрута.ТекущийЭлемент;

	Если Не ЗначениеЗаполнено(БизнесПроцесс) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо указать бизнес-процесс.'"),,
			"БизнесПроцесс");
		Возврат;
	КонецЕсли;
	
	Если ТекЭлемент = Неопределено ИЛИ
		НЕ (ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыДействие")
		ИЛИ ТипЗнч(ТекЭлемент) = Тип("ЭлементГрафическойСхемыВложенныйБизнесПроцесс")) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Необходимо выбрать точку действия или вложенный бизнес-процесс карты маршрута.'"),,
			"КартаМаршрута");
		Возврат;
	КонецЕсли;

	ЗаголовокФормы = НСтр("ru = 'Задачи по точке маршрута бизнес-процесса'");
	
	ОткрытьФорму("Задача.ЗадачаИсполнителя.ФормаСписка", 
		Новый Структура("Отбор,ЗаголовокФормы,ПоказыватьЗадачи,ВидимостьОтборов,БлокировкаОкнаВладельца,Задача,БизнесПроцесс", 
			Новый Структура("БизнесПроцесс,ТочкаМаршрута", БизнесПроцесс, ТекЭлемент.Значение),
			ЗаголовокФормы,0,Ложь,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца,Строка(ТекЭлемент.Значение),Строка(БизнесПроцесс)),
		ЭтаФорма, БизнесПроцесс);


КонецПроцедуры

#КонецОбласти
