
#Область ПрограммныйИнтерфейс
// Процедура создает/перезаписывает запись о статусе элемента справочника...
// в регистре сведений нсиСтатусыОбработкиСправочников.
// Параметры:
//	Справочник - ссылка на элемент справочника;
//	Параметры - структура, которая может содержать следующие свойства:
//		Пользователь - ссылка на элемент справочника "Пользователи";
//		ВременныйЭлемент - булево;
//		СозданаЗаявка - булево;
//		ОбработкаНачата - булево;
//		Обработано - булево;
//		ИзменяемыйОбъект - ссылка на элемент справочника.
//
Процедура УстановитьСтатусСправочника(Справочник, Параметры) Экспорт 
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.нсиСтатусыОбработкиСправочников");
	ЭлементБлокировки.УстановитьЗначение("Объект", Справочник);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Блокировка.Заблокировать(); 
	
	МенеджерЗаписи = РегистрыСведений.нсиСтатусыОбработкиСправочников.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект 			= Справочник;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Объект 			= Справочник;
	Если Параметры.Свойство("Пользователь") Тогда 
		МенеджерЗаписи.Пользователь 	= Параметры.Пользователь;
	КонецЕсли;
	Если Параметры.Свойство("ВременныйЭлемент") Тогда 
		МенеджерЗаписи.ВременныйЭлемент = Параметры.ВременныйЭлемент;
	КонецЕсли;
	Если Параметры.Свойство("СозданаЗаявка") Тогда 
		МенеджерЗаписи.СозданаЗаявка 	= Параметры.СозданаЗаявка;
	КонецЕсли;
	Если Параметры.Свойство("ОбработкаНачата") Тогда 
		МенеджерЗаписи.ОбработкаНачата 	= Параметры.ОбработкаНачата;
		МенеджерЗаписи.Обработано = Ложь;
	КонецЕсли;
	Если Параметры.Свойство("Обработано") Тогда 
		Если Параметры.Обработано Тогда 
			МенеджерЗаписи.Обработавший 	= ПараметрыСеанса.ТекущийПользователь;
			МенеджерЗаписи.ДатаОбработки 	= ТекущаяДата();
		Иначе
			МенеджерЗаписи.Обработавший 	= Неопределено;
			МенеджерЗаписи.ДатаОбработки 	= Дата(1,1,1);
		КонецЕсли;
		МенеджерЗаписи.Обработано 		= Параметры.Обработано;
	КонецЕсли;            	
	Если Параметры.Свойство("ИзменяемыйОбъект") Тогда
		МенеджерЗаписи.ИзменяемыйОбъект = Параметры.ИзменяемыйОбъект;	
	КонецЕсли;	
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры	

// Процедура - определяет как редактируется форма (только новая или адресованная пользователю).
//
Процедура ОпределитьДоступКФорме(Ссылка, ТолькоПросмотр, ВременныйЭлемент) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	
	// новые записи при первичной обработке
	Если Ссылка.Пустая() И Константы.нсиВестиРучнуюОбработкуСправочников.Получить() Тогда 
		ТолькоПросмотр = Ложь;
		Возврат;
	КонецЕсли;	
	
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.нсиУниверсальныйКлассификатор")
		ИЛИ ТипЗнч(Ссылка) = Тип("СправочникСсылка.нсиУниверсальныйФункциональныйСправочник") Тогда 
		
		ИспользоватьЗаявки = Ссылка.Владелец.ИспользоватьЗаявки;
		ИспользоватьБуфер = Ссылка.Владелец.ИспользоватьНормализацию;
	Иначе
		
		МЗ = РегистрыСведений.нсиДоступностьОперацийПоТипамОбъектов.СоздатьМенеджерЗаписи();
		МЗ.ТипОбъекта = "Справочник."+Ссылка.Метаданные().Имя;
		МЗ.Прочитать();
		ИспользоватьЗаявки = МЗ.ИспользоватьЗаявки;
		ИспользоватьБуфер = МЗ.ИспользоватьБуфер;
	КонецЕсли;
	
	ТолькоПросмотр = Не нсиСравнениеДанныхСервер.ПроверитьРедактированиеСправочниковБезЗаявок(Ссылка);
	
	
	Если ИспользоватьЗаявки ИЛИ ИспользоватьБуфер Тогда 
	
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ Пользователь, ВременныйЭлемент, ОбработкаНачата
			|ИЗ РегистрСведений.нсиСтатусыОбработкиСправочников ГДЕ Объект = &Объект");
		Запрос.УстановитьПараметр("Объект", Ссылка);
		
		ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.Пользователь = ПараметрыСеанса.ТекущийПользователь 
				И (ВременныйЭлемент ИЛИ ВыборкаДетальныеЗаписи.ВременныйЭлемент ИЛИ ВыборкаДетальныеЗаписи.ОбработкаНачата) Тогда 
				ТолькоПросмотр = Ложь;
			КонецЕсли;	
		КонецЦикла;  
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает изменяемый объект.
//
Функция ИзменяемыйОбъект(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	нсиСтатусыОбработкиСправочников.ИзменяемыйОбъект
		|ИЗ
		|	РегистрСведений.нсиСтатусыОбработкиСправочников КАК нсиСтатусыОбработкиСправочников
		|ГДЕ
		|	нсиСтатусыОбработкиСправочников.Объект = &Объект";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() тогда
		Возврат Неопределено
	Иначе 
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Возврат Выборка.ИзменяемыйОбъект
	КонецЕсли;	
	
КонецФункции	

#КонецОбласти

