
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого Справочник Из Метаданные.Справочники Цикл
		Если Метаданные.ОпределяемыеТипы.нсиСправочникиВедущиесяПоЗаявкам.Тип.СодержитТип(Тип("СправочникСсылка."+Справочник.Имя)) 
			И Справочник.Имя <> "нсиУниверсальныйФункциональныйСправочник"
			И Справочник.Имя <> "нсиУниверсальныйКлассификатор" Тогда
			
			Элементы.ТипОбъекта.СписокВыбора.Добавить("Справочник."+Справочник.Имя,Справочник.Синоним);
		КонецЕсли;
	КонецЦикла;
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОбъектаПриИзменении(Элемент)
	РежимыРаботы = ПолучитьДоступныеРежимыРаботы(Запись.ТипОбъекта);
	
	Если НЕ РежимыРаботы.Буфер Тогда 
		Запись.ИспользоватьБуфер = Ложь;
	КонецЕсли;
	
	Если НЕ РежимыРаботы.Заявки Тогда 
		Запись.ИспользоватьЗаявки = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	Если ЗначениеЗаполнено(Запись.ТипОбъекта) Тогда 
		РежимыРаботы = ПолучитьДоступныеРежимыРаботы(Запись.ТипОбъекта);
		Элементы.ИспользоватьБуфер.Доступность = РежимыРаботы.Буфер;
		Элементы.ИспользоватьЗаявки.Доступность = РежимыРаботы.Заявки;
		Элементы.ИспользоватьРучнуюОбработкуСправочников.Доступность = Истина;
	Иначе
		Элементы.ИспользоватьБуфер.Доступность = Ложь;
		Элементы.ИспользоватьЗаявки.Доступность = Ложь;
		Элементы.ИспользоватьРучнуюОбработкуСправочников.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДоступныеРежимыРаботы(ТипОбъекта)
	Результат = новый Структура("Буфер,Заявки",Ложь,Ложь);
	Попытка
		ИмяСправочника = СтрЗаменить(ТипОбъекта,"Справочник.","");
		Результат.Заявки = Метаданные.ОпределяемыеТипы.нсиСправочникиВедущиесяПоЗаявкам.Тип.СодержитТип(Тип("СправочникСсылка."+ИмяСправочника));
		Результат.Буфер = Метаданные.ОпределяемыеТипы.нсиСправочникиСНормализацией.Тип.СодержитТип(Тип("СправочникСсылка."+ИмяСправочника));
	Исключение
	КонецПопытки;
	Возврат Результат;
КонецФункции

#КонецОбласти
