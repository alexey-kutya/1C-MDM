#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	нсиБизнесПроцессы.ЗаполнитьСписокСправочниковВедущихсяПоЗаявкам(Элементы.ИмяСправочника.СписокВыбора);
	УправлениеВидимостьюИДоступом();
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.Использовать Тогда 
		Если ТекущийОбъект.Заявитель = Справочники.ГруппыПользователей.ВсеПользователи Тогда 
			Запрос = Новый Запрос(
				"ВЫБРАТЬ ПЕРВЫЕ 2
				|	1 КАК Поле1
				|ИЗ
				|	РегистрСведений.нсиНастройкиБПДляСправочников КАК нсиНастройкиБПДляСправочников
				|ГДЕ
				|	нсиНастройкиБПДляСправочников.ИмяСправочника = &ИмяСправочника
				|	И нсиНастройкиБПДляСправочников.НастройкаБП.ИмяБП = &ИмяБП
				|	И нсиНастройкиБПДляСправочников.Использовать
				|	И нсиНастройкиБПДляСправочников.Заявитель В (
				|		НЕОПРЕДЕЛЕНО,
				|		ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка),
				|		ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ПустаяСсылка),
				|		ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
				|	)
				|"
			);
			Запрос.УстановитьПараметр("ИмяБП",ТекущийОбъект.НастройкаБП.ИмяБП);
			Запрос.УстановитьПараметр("ИмяСправочника",ТекущийОбъект.ИмяСправочника);
			Запрос.УстановитьПараметр("Заявитель",ТекущийОбъект.Заявитель);
			Если Запрос.Выполнить().Выбрать().Количество()>1 Тогда 
				ТекстСообщения = 
					"Для справочника """+ТекущийОбъект.ИмяСправочника
					+""" и бизнес-процесса """+ТекущийОбъект.НастройкаБП.ИмяБП
					+""" уже есть используемая настройка по умолчанию.";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаБППриИзменении(Элемент)
	УправлениеВидимостьюИДоступом();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеВидимостьюИДоступом()
	Элементы.ОтборПредметов.Видимость = Запись.НастройкаБП.ИмяБП <> "нсиПакетныйВводЭлементовСправочника";
КонецПроцедуры

#КонецОбласти
