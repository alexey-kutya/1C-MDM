
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.GlobalSKU") Тогда
		
		// заполнение реквизитов
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , "Код, Владелец, Родитель, ЭтоМакет");
		
		// заполнение регистров сведений
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект", ДанныеЗаполнения.Ссылка);
		Запрос.УстановитьПараметр("ОбъектЗагрузки", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ &ОбъектЗагрузки КАК Объект, Свойство, Значение
		|ИЗ РегистрСведений.ДополнительныеСведения ГДЕ Объект = &Объект"; 
		Результат = Запрос.Выполнить().Выгрузить();
		Если Не Результат.Количество() = 0 Тогда 
			НаборЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НаборЗаписи.Отбор.Объект.Установить(Ссылка);
			НаборЗаписи.Загрузить(Результат);
			НаборЗаписи.Записать(Истина); 			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтотОбъект.ЭтоМакет Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	GlobalSKU.Представление
		|ИЗ
		|	Справочник.GlobalSKU КАК GlobalSKU
		|ГДЕ
		|	GlobalSKU.ID = &ID
		|	И GlobalSKU.ЭтоМакет = ЛОЖЬ
		|	И НЕ GlobalSKU.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("ID", ID);
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекстСообщения = НСтр("ru = 'Элемент с таким идентификатором уже существует.'; en = 'Item with this ID already exists'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Черновик Тогда
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли; 
	
КонецПроцедуры
