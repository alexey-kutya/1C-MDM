///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПропуститьВыборПроизводственногоКалендаря = Ложь;
	
	ЗаполнитьСписокПроизводственныхКалендарей(ПропуститьВыборПроизводственногоКалендаря);
	
	Если ПропуститьВыборПроизводственногоКалендаря Тогда
		Элементы.ГруппаВыборПроизводственногоКалендаря.Видимость = Ложь;
	КонецЕсли;
	
	ЗаполнитьВидКалендаря();
	
	ЗаполнитьВидимыеСтраницы();
	
	УстановитьТекущуюСтраницу();
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВидимыеСтраницы.Количество() = 0 Тогда
		Готово();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидимыеСтраницы()
	
	Страницы = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы;
	
	МассивСтраниц = Новый Массив;
	Для ИндексСтраницы = 0 По Страницы.Количество() - 1 Цикл
		Страница = Страницы[ИндексСтраницы];
		Если Страница.Видимость Тогда 
			МассивСтраниц.Добавить(ИндексСтраницы);
		КонецЕсли;
	КонецЦикла;
	
	ВидимыеСтраницы = Новый ФиксированныйМассив(МассивСтраниц);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтраницу()
	
	Если ВидимыеСтраницы.Количество() > 0 Тогда
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Получить(ВидимыеСтраницы.Получить(0));
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ЗАПОЛНЕНИЯ ФОРМЫ

&НаСервере
Процедура ЗаполнитьСписокПроизводственныхКалендарей(ПропускатьВыборПроизводственногоКалендаря)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПроизводственныеКалендари.Ссылка КАК ПроизводственныйКалендарь
	|ИЗ
	|	Справочник.ПроизводственныеКалендари КАК ПроизводственныеКалендари
	|ГДЕ
	|	(НЕ ПроизводственныеКалендари.ПометкаУдаления)");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		
		ВызватьИсключение НСтр("ru = 'Не удалось создать календарь по шаблону. Шаблоны календарей (Производственные календари) не обнаружены.'");
		
	Иначе	
		
		СписокПроизводственныхКалендарей.Загрузить(РезультатЗапроса.Выгрузить());
		
		Если СписокПроизводственныхКалендарей.Количество() = 1 Тогда
			ПропускатьВыборПроизводственногоКалендаря = Истина;
			ПроизводственныйКалендарь = СписокПроизводственныхКалендарей[0].ПроизводственныйКалендарь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидКалендаря()
	
	ВидКалендаря = Перечисления.ВидыКалендарей.Пятидневка;
	
КонецПроцедуры

&НаСервере
Процедура СменитьСтраницу(Направление)
	
	ИндексТекущейСтраницы 	= ВидимыеСтраницы.Найти(ИндексСтраницы(Элементы.ГруппаСтраницыФормы.ТекущаяСтраница));
	ИндексНовойСтраницы 	= ИндексТекущейСтраницы + Направление;
	
	Если ИндексНовойСтраницы >= 0 И ИндексНовойСтраницы < ВидимыеСтраницы.Количество() Тогда
		Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Получить(ВидимыеСтраницы.Получить(ИндексНовойСтраницы));
	КонецЕсли;
	
	УстановитьСвойстваЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	ИндексТекущейСтраницы = ИндексСтраницы(Элементы.ГруппаСтраницыФормы.ТекущаяСтраница);
	ЭтоПоследняяСтраница = ВидимыеСтраницы.Найти(ИндексТекущейСтраницы) = ВидимыеСтраницы.ВГраница();
	
	Элементы.ФормаДалее.Заголовок = ?(ЭтоПоследняяСтраница, Нстр("ru = 'Готово'"), Нстр("ru = 'Далее >>'"));
	Элементы.ФормаНазад.Видимость = НЕ ВидимыеСтраницы.Найти(ИндексТекущейСтраницы) = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура Готово()
	Закрыть(Новый Структура("ПроизводственныйКалендарь, ВидКалендаря", ПроизводственныйКалендарь, ВидКалендаря));
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ИндексСтраницы(Страница)
	
	Возврат Элементы.ГруппаСтраницыФормы.ПодчиненныеЭлементы.Индекс(Страница);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ
	
&НаКлиенте
Процедура Далее(Команда)

	Если ЭтоПоследняяСтраница Тогда
		Готово();
	Иначе
		СменитьСтраницу(1);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	СменитьСтраницу(-1);	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроизводственныхКалендарейПриАктивизацииСтроки(Элемент)
	
	ПроизводственныйКалендарь = Элементы.СписокПроизводственныхКалендарей.ТекущиеДанные.ПроизводственныйКалендарь;
	
КонецПроцедуры
