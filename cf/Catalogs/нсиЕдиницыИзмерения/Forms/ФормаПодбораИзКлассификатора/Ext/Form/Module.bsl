#Область ОбработчикиСобытийФормы

&НаСервере
// Обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = Справочники.нсиЕдиницыИзмерения.ПолучитьМакет("КлассификаторЕдиницИзмерения");
	
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка
	
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.Вывести(Макет);
	
	// Пользователю не показываем весовая это или нет единица измерения.
	ТабличныйДокумент.Области.ВесоваяЕдиница.Очистить();

	ТабличныйДокумент.ФиксацияСверху      = ТабличныйДокумент.Области.ОбластьРасшифровки.Верх - 1;

	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ТолькоПросмотр      = Истина;

КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
// Обработчик события ОбработкаРасшифровки элемента ПолеТабличногоДокумента.
//
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	// Получение значений полей выбранной строки.
	ТекущаяОбласть = Элементы.ТабличныйДокумент.ТекущаяОбласть;
	
	ОбластьКодЧисловой             = ТабличныйДокумент.Области.КодЧисловой;
	ОбластьНаименованиеКраткое     = ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное      = ТабличныйДокумент.Области.НаименованиеПолное;
	ОбластьМеждународноеСокращение = ТабличныйДокумент.Области.МеждународноеСокращение;
	
	КодЧисловой             = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.            Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.Право).Текст;
	НаименованиеКраткое     = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.    Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст;
	НаименованиеПолное      = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное.     Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное.Право).Текст;
	МеждународноеСокращение = ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьМеждународноеСокращение.Лево, ТекущаяОбласть.Низ, ОбластьМеждународноеСокращение.Право).Текст;
	
	// Проверка наличия выбранной единицы измерения.
	Ссылка = НайтиПоКодуКлассификаторЕдиницИзмерения(КодЧисловой);
			
	Если НЕ Ссылка.Пустая() Тогда
		
		Вопрос = НСтр("ru = 'В справочнике ""Классификатор единиц измерения"" уже существует элемент с кодом """ + КодЧисловой + """! Открыть существующий?'");
		ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаОтветаНаВопросОДублированииКода",ЭтаФорма, Ссылка);
		ПоказатьВопрос(ОписаниеОповещения, Вопрос, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет,);
		
		Возврат;
		
	КонецЕсли;
		
	// Создание новой единицы измерения.
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Код", КодЧисловой);
	ПараметрыОткрытия.Вставить("Наименование", СтрПолучитьСтроку(НаименованиеКраткое, 1));
	ПараметрыОткрытия.Вставить("МеждународноеСокращение", Лев(МеждународноеСокращение, 3));
	ПараметрыОткрытия.Вставить("НаименованиеПолное", СтрПолучитьСтроку(НаименованиеПолное, 1));
			
	ОткрытьФорму("Справочник.нсиЕдиницыИзмерения.Форма.ФормаЭлемента", ПараметрыОткрытия);
	    	
КонецПроцедуры // ПолеТабличногоДокументаОбработкаРасшифровки()

&НаКлиенте
Процедура ОбработкаОтветаНаВопросОДублированииКода(Результат,Ссылка) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		 ПараметрыОткрытия = Новый Структура("Ключ", Ссылка);
		 ОткрытьФорму("Справочник.нсиЕдиницыИзмерения.Форма.ФормаЭлемента", ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Функция возвращает ссылку классификатора единиц измерения по числовому коду.
//
Функция НайтиПоКодуКлассификаторЕдиницИзмерения(КодЧисловой)
	
	 Возврат Справочники.нсиЕдиницыИзмерения.НайтиПоКоду(КодЧисловой);
	
КонецФункции // НайтиПоКодуКлассификаторЕдиницИзмерения()	

#КонецОбласти
