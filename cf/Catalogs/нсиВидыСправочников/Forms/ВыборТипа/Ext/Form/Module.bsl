#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Типы = Новый ДеревоЗначений;
	Типы.Колонки.Добавить("Представление");
	Типы.Колонки.Добавить("НеВыбирать");
	Типы.Колонки.Добавить("Тип");
	
	ОрганичениеТипа = Неопределено;
	Если не ПустаяСтрока(Параметры.ОграничениеТипа) Тогда
		ОрганичениеТипа = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Параметры.ОграничениеТипа);
	КонецЕсли;
	
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Булево", "Булево");
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Число", "Число");
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Строка", "Строка");
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Дата", "Дата");
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Время", "Время");
	ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Дата и время", "ДатаИВремя");
	ДобавитьРодныеТипы(ОрганичениеТипа, Типы.Строки, "Справочник", "Справочник", Метаданные.Справочники);
	ДобавитьРодныеТипы(ОрганичениеТипа, Типы.Строки, "Перечисление", "Перечисление", Метаданные.Перечисления);
	
	СтрокаДерева = ДобавитьТип(ОрганичениеТипа, Типы.Строки, "Универсальное хранилище", "Хранилище", Истина);
	Если СтрокаДерева<>Неопределено Тогда
		Запрос = Новый Запрос();
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	нсиВидыСправочников.Наименование,
			|	нсиВидыСправочников.Ссылка
			|ИЗ
			|	Справочник.нсиВидыСправочников КАК нсиВидыСправочников
			|ГДЕ
			|	нсиВидыСправочников.ВидСправочника <> ЗНАЧЕНИЕ(Перечисление.нсиВидыСправочников.ТабличнаяЧасть)
			|	И НЕ нсиВидыСправочников.ПометкаУдаления
			|	//ОграничениеТипаХранилище
			|УПОРЯДОЧИТЬ ПО
			|	нсиВидыСправочников.Наименование";
					   
		Если Параметры.ОграничениеТипаХранилище.Количество()>0 Тогда 
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ОграничениеТипаХранилище", "И нсиВидыСправочников.Ссылка В (&ОграничениеТипаХранилище)");
			Запрос.УстановитьПараметр("ОграничениеТипаХранилище",Параметры.ОграничениеТипаХранилище.ВыгрузитьЗначения());
		КонецЕсли;
						   
		Выб = Запрос.Выполнить().Выбрать();
		Пока Выб.Следующий() Цикл
			ДобавитьТип(Неопределено, СтрокаДерева.Строки, Выб.Наименование, Выб.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(Типы, "ДеревоТипов");
	
	ТекСтрока = Неопределено;
	СтТип = нсиУниверсальноеХранилище.РазобратьТип1(Параметры.Тип1);
	Длина = СтТип.Длина;
	Точность = СтТип.Точность;
	
	СтрокаТипа = Типы.Строки.Найти(СтТип.Тип1, "Тип");
	Если СтрокаТипа<>Неопределено Тогда
		ЭлементДерева = ДеревоТипов.ПолучитьЭлементы()[Типы.Строки.Индекс(СтрокаТипа)];
		Если Параметры.Тип2=Неопределено Тогда
			ТекСтрока = ЭлементДерева.ПолучитьИдентификатор();
		Иначе
			СтрокаПодтипа = СтрокаТипа.Строки.Найти(Параметры.Тип2, "Тип");
			Если СтрокаПодтипа<>Неопределено Тогда
				ТекСтрока = ЭлементДерева.ПолучитьЭлементы()[СтрокаТипа.Строки.Индекс(СтрокаПодтипа)].ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТекСтрока<>Неопределено Тогда
		Элементы.ДеревоТипов.ТекущаяСтрока = ТекСтрока;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДеревоТиповПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные.Тип="Строка" Тогда
		Элементы.Длина.Доступность = Истина;
		Элементы.Точность.Доступность = Ложь;
	ИначеЕсли Элемент.ТекущиеДанные.Тип="Число" Тогда
		Элементы.Длина.Доступность = Истина;
		Элементы.Точность.Доступность = Истина;
	Иначе
		Элементы.Длина.Доступность = Ложь;
		Элементы.Точность.Доступность = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоТиповВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Отказ = Ложь;
	ПроверкаВыбора(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	ВыполнитьВыбор();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Отказ = Ложь;
	ПроверкаВыбора(Отказ);
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Недопустимый тип.");
		Возврат;
	КонецЕсли;
	ВыполнитьВыбор();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ДобавитьТип(ОрганичениеТипа, Строки, Представление, Тип, НеВыбирать = Ложь)
	Если ОрганичениеТипа<>Неопределено Тогда
		Если ОрганичениеТипа.Найти(Тип)=Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	СтрокаТипа = Строки.Добавить();
	СтрокаТипа.Представление = Представление;
	СтрокаТипа.Тип = Тип;
	СтрокаТипа.НеВыбирать = НеВыбирать;
	Возврат СтрокаТипа;
КонецФункции

&НаСервере
Процедура ДобавитьРодныеТипы(ОрганичениеТипа, Строки, Представление, Имя, КоллекцияМетаданных)
	СтрокаДерева = ДобавитьТип(ОрганичениеТипа, Строки, Представление, Имя, Истина);
	Если СтрокаДерева=Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для каждого МД из КоллекцияМетаданных Цикл
		ДобавитьТип(Неопределено, СтрокаДерева.Строки, МД.Представление(), МД.Имя);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВыбора(Отказ)
	ТекДанные = Элементы.ДеревоТипов.ТекущиеДанные;
	Если ТекДанные.Тип="Строка" Тогда
		Если Длина=0 или Длина>1000 Тогда
			Отказ = Истина;
		КонецЕсли;
	ИначеЕсли ТекДанные.Тип="Число" Тогда
		Если Длина=0 или Длина > 16 или Точность>4 или Точность>=Длина Тогда
			Отказ = Истина;
		КонецЕсли;
	ИначеЕсли ТекДанные.НеВыбирать Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()
	ТекДанные = Элементы.ДеревоТипов.ТекущиеДанные;
	Если ТекДанные.ПолучитьРодителя()=Неопределено Тогда
		Тип1 = ТекДанные.Тип;
		Тип2 = Неопределено;
		Если Тип1="Строка" Тогда
			Тип1 = Тип1+"("+Формат(Длина, "ЧГ=;ЧН=")+")";
		ИначеЕсли Тип1="Число" Тогда
			Тип1 = Тип1+"("+Формат(Длина, "ЧГ=;ЧН=")+","+Формат(Точность, "ЧГ=;ЧН=")+")";
		КонецЕсли;
	Иначе
		Тип1 = ТекДанные.ПолучитьРодителя().Тип;
		Тип2 = ТекДанные.Тип;
	КонецЕсли;
	ОповеститьОВыборе(Новый Структура("Тип1,Тип2", Тип1, Тип2));
КонецПроцедуры

#КонецОбласти