
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодчиненныеРеквизиты = Новый Структура;
	ПодчиненныеРеквизиты.Вставить("LocalMaterialType", "LocalBrand");
	ПодчиненныеРеквизиты.Вставить("LocalBrand", "РазновидностьВкус");
	ПодчиненныеРеквизиты.Вставить("РазновидностьВкус", "LocalVersion");
	ПодчиненныеРеквизиты.Вставить("LocalVersion", "LocalAlcoholContent");
	ПодчиненныеРеквизиты.Вставить("LocalAlcoholContent", "LocalVolume");
	
	ПустыеСсылки = Новый Структура;
	ПустыеСсылки.Вставить("LocalMaterialType", ПредопределенноеЗначение("Справочник.LocalMaterialType.ПустаяСсылка"));
	ПустыеСсылки.Вставить("LocalBrand", ПредопределенноеЗначение("Справочник.LocalBrand.ПустаяСсылка"));
	ПустыеСсылки.Вставить("РазновидностьВкус", ПредопределенноеЗначение("Справочник.РазновидностиВкусы.ПустаяСсылка"));
	ПустыеСсылки.Вставить("LocalVersion", ПредопределенноеЗначение("Справочник.LocalVersion.ПустаяСсылка"));
	ПустыеСсылки.Вставить("LocalAlcoholContent", ПредопределенноеЗначение("Справочник.LocalAlcoholContent.ПустаяСсылка"));
	ПустыеСсылки.Вставить("LocalVolume", ПредопределенноеЗначение("Справочник.LocalVolume.ПустаяСсылка"));
	
	УстановитьДоступность();
	
	УстановитьОтборыПоРеквизитам();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отбор = ЕдиницыИзмерения.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = ЕдиницыИзмерения.Отбор.ДоступныеПоляОтбора.Элементы.Найти("Владелец").Поле;
	Отбор.ПравоеЗначение = Объект.Ссылка;
	
	Отбор = Штрихкоды.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Штрихкоды.Отбор.ДоступныеПоляОтбора.Элементы.Найти("Владелец").Поле;
	Отбор.ПравоеЗначение = Объект.Ссылка;
	
	Отбор = Спецификации.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Спецификации.Отбор.ДоступныеПоляОтбора.Элементы.Найти("Номенклатура").Поле;
	Отбор.ПравоеЗначение = Объект.Ссылка;
	
	Если Параметры.Ключ.Пустая()
		И ЗначениеЗаполнено(ПараметрыСеанса.ГруппаУправленияНСИ) Тогда
		
		СтранаОрганизации = ПараметрыСеанса.ГруппаУправленияНСИ.СтранаОрганизации;
		
		Объект.СтранаОрганизации = СтранаОрганизации;
		Объект.СтранаОрганизацииРазделительДанных = СтранаОрганизации.Код;
		
	КонецЕсли; 
	
	
КонецПроцедуры

&НаКлиенте
Процедура LocalMaterialTypeПриИзменении(Элемент)
	
	УстановитьОтборПодчиненного(Элемент.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура LocalBrandПриИзменении(Элемент)
	
	УстановитьОтборПодчиненного(Элемент.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РазновидностьВкусПриИзменении(Элемент)
	
	УстановитьОтборПодчиненного(Элемент.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура LocalVersionПриИзменении(Элемент)
	
	УстановитьОтборПодчиненного(Элемент.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура LocalAlcoholContentПриИзменении(Элемент)
	
	УстановитьОтборПодчиненного(Элемент.Имя, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборыПоРеквизитам()

	Для каждого КлючИЗначение Из ПодчиненныеРеквизиты Цикл
		УстановитьОтборПодчиненного(КлючИЗначение.Ключ);
	КонецЦикла; 

КонецПроцедуры // УстановитьОтборы()

&НаКлиенте
Процедура УстановитьОтборПоРеквизиту(СписокДляОтбора, ИмяРеквизита)

	НовыйПараметр = Новый ПараметрВыбора("Отбор.Ссылка", СписокДляОтбора);
    НовыйМассив = Новый Массив();
    НовыйМассив.Добавить(НовыйПараметр);
    НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
    Элементы[ИмяРеквизита].ПараметрыВыбора = НовыеПараметры;

КонецПроцедуры // УстановитьОтбор()

&НаСервереБезКонтекста
Функция СписокПодчиненныхОбъектов(ОбъектОтбора, ИмяИзмеренияОтбора, ИмяИзмеренияВыбора)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СоответствиеКлючевыхАтрибутов.ИмяИзмеренияВыбора
		|ИЗ
		|	РегистрСведений.СоответствиеКлючевыхАтрибутов КАК СоответствиеКлючевыхАтрибутов
		|ГДЕ
		|	СоответствиеКлючевыхАтрибутов.ИмяИзмеренияОтбора = &ОбъектОтбора
		|
		|СГРУППИРОВАТЬ ПО
		|	СоответствиеКлючевыхАтрибутов.ИмяИзмеренияВыбора";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяИзмеренияОтбора", ИмяИзмеренияОтбора);	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяИзмеренияВыбора", ИмяИзмеренияВыбора);	
	
	Запрос.УстановитьПараметр("ОбъектОтбора", ОбъектОтбора);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку(ИмяИзмеренияВыбора);

КонецФункции // СписокРазновидностей()

&НаКлиенте
Процедура УстановитьОтборПодчиненного(ИмяРеквизита, ОчищатьПодчиненныйРеквизит = Ложь)
	
	ИмяПодчиненногоРеквизита = ПодчиненныеРеквизиты[ИмяРеквизита];
	
	Если ОчищатьПодчиненныйРеквизит Тогда
		Объект[ИмяПодчиненногоРеквизита] = ПустыеСсылки[ИмяПодчиненногоРеквизита];
	КонецЕсли; 
	
	УстановитьОтборПоРеквизиту(СписокПодчиненныхОбъектов(Объект[ИмяРеквизита], ИмяРеквизита, ИмяПодчиненногоРеквизита), ИмяПодчиненногоРеквизита);
	
	Если ПодчиненныеРеквизиты.Свойство(ИмяПодчиненногоРеквизита) Тогда
		УстановитьОтборПодчиненного(ИмяПодчиненногоРеквизита, ОчищатьПодчиненныйРеквизит);		
	КонецЕсли; 

КонецПроцедуры // ПрименитьУниверсальныйОтбор()
 
&НаКлиенте
Процедура УстановитьДоступность()

	Если Параметры.Ключ.Пустая()
		ИЛИ НЕ ЗначениеЗаполнено(Объект.Status) Тогда
		Элементы.LocalSKUStatus.Доступность = Истина;
	Иначе
		Элементы.LocalSKUStatus.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры // УстановитьДоступность()

&НаКлиенте
Процедура СтранаОрганизацииПриИзменении(Элемент)
	Объект.СтранаОрганизацииРазделительДанных = GlobalMDM.ПолучитьРеквизитНаСервере(Объект.СтранаОрганизации, "Код");
КонецПроцедуры

