#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.нсиМТР") Тогда
		
		// заполнение реквизитов
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , "Владелец,Родитель,Код,ЭтоМакет");
		
		Если ЭтоГруппа Тогда 
			Возврат;
		КонецЕсли;	
		
		// заполнение табличных частей
		АльтернативныеЕдиницыИзмерения.Загрузить(ДанныеЗаполнения.АльтернативныеЕдиницыИзмерения.Выгрузить());
		ДополнительнаяКлассификация.Загрузить(ДанныеЗаполнения.ДополнительнаяКлассификация.Выгрузить());
		ДополнительныеРеквизиты.Загрузить(ДанныеЗаполнения.ДополнительныеРеквизиты.Выгрузить());
		ТехническиеХарактеристики.Загрузить(ДанныеЗаполнения.ТехническиеХарактеристики.Выгрузить());
		Аналоги.Загрузить(ДанныеЗаполнения.Аналоги.Выгрузить());
		Запчасти.Загрузить(ДанныеЗаполнения.Запчасти.Выгрузить());
		
		// заполнение регистров сведений
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект", ДанныеЗаполнения.Ссылка);
		Запрос.УстановитьПараметр("ОбъектЗагрузки", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ &ОбъектЗагрузки КАК Объект, Свойство, Значение
		|ИЗ РегистрСведений.ДополнительныеСведения ГДЕ Объект = &Объект"; 
		Результат = Запрос.Выполнить().Выгрузить();
		Если Не Результат.Количество() = 0 Тогда 
			НаборЗаписи = РегистрыСведений.ДополнительныеСведения.СоздатьНаборЗаписей();
			НаборЗаписи.Отбор.Объект.Установить(Ссылка);
			НаборЗаписи.Загрузить(Результат);
			НаборЗаписи.Записать(Истина); 			
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Объект", ДанныеЗаполнения.Ссылка);
		Запрос.УстановитьПараметр("ОбъектЗагрузки", Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ Период, &ОбъектЗагрузки КАК МТР, Поставщик, Организация, Цена, Валюта
		|ИЗ РегистрСведений.нсиЦеныПоставщиков ГДЕ МТР = &Объект"; 
		Результат = Запрос.Выполнить().Выгрузить();
		Если Не Результат.Количество() = 0 Тогда 
			НаборЗаписи = РегистрыСведений.нсиЦеныПоставщиков.СоздатьНаборЗаписей();
			НаборЗаписи.Отбор.МТР.Установить(Ссылка);
			НаборЗаписи.Загрузить(Результат);
			НаборЗаписи.Записать(Истина); 			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	#Если Сервер ИЛИ ВнешнееСоединение тогда
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюВидаОбъекта(ЭтотОбъект, "Справочник_нсиМТР");
	// Конец СтандартныеПодсистемы.Свойства
	#КонецЕсли
	
//	КутьяАА ITRR	
	//Если НЕ СтранаОрганизацииРазделительДанных = СтранаОрганизации.Код Тогда
	//
	//	СтранаОрганизацииРазделительДанных = СтранаОрганизации.Код;
	//
	//КонецЕсли; 
	//	КутьяАА ITRR	
	
	//фыв++ костыль в виде условия
	Если ТипЗнч(ЭтотОбъект)=Тип("СправочникОбъект.нсиМТР") И ЭтотОбъект.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	//--
	
	нсиСравнениеДанныхСобытия.ПроверитьТипПозицииПередЗаписьюФункциональногоСправочника(ЭтотОбъект,Отказ);
	нсиОбщегоНазначения.УдалитьПробелыИзСтроки(Наименование);
	нсиОбщегоНазначения.УдалитьПробелыИзСтроки(ПолноеНаименование);
	
	Если ЭтоМакет тогда
		ДополнительныеСвойства.Вставить("СШПНеобрабатывать", Истина); 
	КонецЕсли;
	
//	КутьяАА ITRR	
	Если НЕ ЗначениеЗаполнено(ОсновнойПроизводитель)
		И GlobalID.Количество() Тогда
		Отказ = Истина;
		ЗаписьЖурналаРегистрации("Запись объекта",УровеньЖурналаРегистрации.Ошибка, Метаданные(),Ссылка,"Необходимо задать основного производителя!");
	КонецЕсли; 

	//фыв++ проверим, изменился ли ОсновнойПроизводитель--> если - да, и старый производитель еще находится в плане обмена, удаляем его из плана
	//т.к. очередность выгрузки неизвестна и старая запись может выгрузиться позже новой (запись пойдет в одни и те же базы)
	Если Ссылка.ОсновнойПроизводитель<>ОсновнойПроизводитель Тогда
//		ДополнительныеСвойства.Вставить("ПроизводительДляУдаления",Ссылка.ОсновнойПроизводитель);
		ДополнительныеСвойства.Вставить("ОсновнойПроизводительИзменен", Истина);
	КонецЕсли; //--
	
	// КутьяАА ITRR <<
	Если Ссылка.Пустая() Тогда
		Если НЕ ЗначениеЗаполнено(Код) Тогда
			УстановитьНовыйКод();
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Артикул) Тогда
			Артикул = Код;
		КонецЕсли; 
	КонецЕсли; 
	//>>
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// проверяются только эталоны
	Если НЕ ПометкаУдаления И
		ТипПозиции = Перечисления.нсиТипыПозицийСправочников.ЭталоннаяПозиция
		Тогда 
		
		// все проверки

	ИначеЕсли ТипПозиции = Перечисления.нсиТипыПозицийСправочников.ДублирующаяПозиция Тогда 	
		
		Если Не ЗначениеЗаполнено(ЭталоннаяПозиция) Тогда 
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Для дублирующей позиции не указан эталон.",ЭтотОбъект, "ЭталоннаяПозиция");
		КонецЕсли;	
		ПроверяемыеРеквизиты.Очистить();
		ПроверяемыеРеквизиты.Добавить("Наименование");
		ПроверяемыеРеквизиты.Добавить("ТипПозиции");
		Возврат; // проверки не работают	
		
	Иначе 
		
// ITRR Кутья АА 
		//ПроверяемыеРеквизиты.Очистить();
		//ПроверяемыеРеквизиты.Добавить("Наименование");
		//ПроверяемыеРеквизиты.Добавить("ТипПозиции");
		Возврат; // проверки не работают	
		
	КонецЕсли;
	
// ITRR Кутья АА 
	Если LocalMaterialType.ГотоваяПродукция Тогда
		ПроверяемыеРеквизиты.Добавить("LocalVolume");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура - устанавливает для дублей новый эталон.
// храним 2х-уровневую таблицу мэппинга.
//
Процедура ПеренестиДублиКНовомуЭталону() Экспорт
	
	Если Не ТипПозиции = Перечисления.нсиТипыПозицийСправочников.ДублирующаяПозиция Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка ИЗ Справочник.нсиМТР ГДЕ ЭталоннаяПозиция = &Ссылка");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.нсиМТР");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ВыборкаДетальныеЗаписи.Ссылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();         		
		
		ИзменяемыйДубль = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ИзменяемыйДубль.ТипПозиции 			= Перечисления.нсиТипыПозицийСправочников.ДублирующаяПозиция;
		ИзменяемыйДубль.ЭталоннаяПозиция 	= ЭталоннаяПозиция;
		ИзменяемыйДубль.Записать();
				
	КонецЦикла;  	
	
КонецПроцедуры	

// Функция - проверяется соответствие исходных характеристик классу и устанавливаются характеристики класса.
//
Функция ПроверитьЗаполнитьТехническиеХарактеристики() Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не Константы.нсиУчитыватьВРазрезеКлассификатораПродукции.Получить() Тогда 
		Возврат Ложь;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(Класс) Тогда 
		Возврат Ложь;
	КонецЕсли;	
	
	// проверяется соответствие исходных характеристик классу
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Класс", Класс);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	нсиХарактеристикиМТР.Ссылка,
		|	нсиМТРТехническиеХарактеристики.Характеристика
		|ИЗ
		|	(ВЫБРАТЬ
		|		нсиМТРТехническиеХарактеристики.Характеристика КАК Характеристика
		|	ИЗ
		|		Справочник.нсиМТР.ТехническиеХарактеристики КАК нсиМТРТехническиеХарактеристики
		|	ГДЕ
		|		нсиМТРТехническиеХарактеристики.Ссылка = &Ссылка) КАК нсиМТРТехническиеХарактеристики
		|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			нсиХарактеристикиМТР.Ссылка КАК Ссылка
		|		ИЗ
		|			ПланВидовХарактеристик.нсиХарактеристикиМТР КАК нсиХарактеристикиМТР
		|		ГДЕ
		|			нсиХарактеристикиМТР.Класс = &Класс
		|			И нсиХарактеристикиМТР.ПометкаУдаления = ЛОЖЬ) КАК нсиХарактеристикиМТР
		|		ПО нсиМТРТехническиеХарактеристики.Характеристика = нсиХарактеристикиМТР.Ссылка
		|ГДЕ
		|	(нсиХарактеристикиМТР.Ссылка ЕСТЬ NULL 
		|			ИЛИ нсиМТРТехническиеХарактеристики.Характеристика ЕСТЬ NULL )";

	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() = 0 Тогда 
		Возврат Ложь;
	КонецЕсли;	

	// установка характеристик класса
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Класс", Класс);
	Запрос.УстановитьПараметр("ТехническиеХарактеристики", ЭтотОбъект.ТехническиеХарактеристики.Выгрузить());
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	нсиМТРТехническиеХарактеристики.Характеристика КАК Характеристика,
		|	нсиМТРТехническиеХарактеристики.Значение КАК Значение
		|ПОМЕСТИТЬ нсиМТРТехническиеХарактеристики
		|ИЗ
		|	&ТехническиеХарактеристики КАК нсиМТРТехническиеХарактеристики
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	нсиХарактеристикиМТР.Ссылка КАК Характеристика,
		|	нсиМТРТехническиеХарактеристики.Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		нсиХарактеристикиМТР.Ссылка КАК Ссылка
		|	ИЗ
		|		ПланВидовХарактеристик.нсиХарактеристикиМТР КАК нсиХарактеристикиМТР
		|	ГДЕ
		|		нсиХарактеристикиМТР.Класс = &Класс) КАК нсиХарактеристикиМТР
		|		ЛЕВОЕ СОЕДИНЕНИЕ нсиМТРТехническиеХарактеристики КАК нсиМТРТехническиеХарактеристики
		//|		ПО (нсиМТРТехническиеХарактеристики.Характеристика.НаименованиеПоКлассификатору = нсиХарактеристикиМТР.Ссылка.НаименованиеПоКлассификатору)
		|		ПО (нсиМТРТехническиеХарактеристики.Характеристика = нсиХарактеристикиМТР.Ссылка)
		|АВТОУПОРЯДОЧИВАНИЕ";

	ТехническиеХарактеристики.Очистить();	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Характеристика.ПометкаУдаления = Истина И Не ЗначениеЗаполнено(Выборка.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Строка = ТехническиеХарактеристики.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Выборка);
		Строка.НаименованиеХарактеристики = Строка.Характеристика.НаименованиеПоКлассификатору;
		
	КонецЦикла;
		
	Возврат Истина;
		
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	
	ЕдиницаХраненияОстатков = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	ЕдиницаДляОтчетов = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	ВидУпаковки = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	ЕдиницаИзмеренияМест = Справочники.ЕдиницыИзмерения.ПустаяСсылка();
	
КонецПроцедуры

#КонецОбласти
