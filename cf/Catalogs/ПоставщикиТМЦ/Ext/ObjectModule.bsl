Процедура ПередЗаписью(Отказ)
	
	Если НЕ ЗаписьУникальна() Тогда
		Отказ = Истина;
		Сообщить("Запись не уникальна", СтатусСообщения.Важное);
	КонецЕсли;  
	
КонецПроцедуры

Функция ЗаписьУникальна()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоставщикиТМЦ.Ссылка
		|ИЗ
		|	Справочник.ПоставщикиТМЦ КАК ПоставщикиТМЦ
		|ГДЕ
		|	ПоставщикиТМЦ.Владелец = &Владелец
		|	И ПоставщикиТМЦ.Организация = &Организация
		|	И ПоставщикиТМЦ.Поставщик = &Поставщик
		|	И ПоставщикиТМЦ.Склад = &Склад
		|	И ПоставщикиТМЦ.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();

КонецФункции // ЗаписьУникальна()

Процедура ПриЗаписи(Отказ)
	
	Если НЕ Отказ Тогда
		Если ОсновнойПоставщик Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоставщикиТМЦ.Ссылка
			|ИЗ
			|	Справочник.ПоставщикиТМЦ КАК ПоставщикиТМЦ
			|ГДЕ
			|	ПоставщикиТМЦ.Владелец = &Владелец
			|	И ПоставщикиТМЦ.Организация = &Организация
			|	И ПоставщикиТМЦ.ОсновнойПоставщик = ИСТИНА
			|	И ПоставщикиТМЦ.Ссылка <> &Ссылка";
			
			Запрос.УстановитьПараметр("Владелец", Владелец);
			Запрос.УстановитьПараметр("Организация", Организация);
				Запрос.УстановитьПараметр("Ссылка", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
					СпрОбъект.ОсновнойПоставщик = Ложь;
					Попытка
						СпрОбъект.Записать();
					Исключение
						Сообщить("Ошибка: "+ОписаниеОшибки(), СтатусСообщения.Важное);
					КонецПопытки;
				КонецЦикла; 
			КонецЕсли; 
			
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры
 
