#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипПредмета = Параметры.ТипПредмета;
	
	нсиБизнесПроцессы.ЗаполнитьСписокСправочниковВедущихсяПоЗаявкам(Элементы.ТипПредмета.СписокВыбора);
	
	ИнициализироватьКомпоновщикНастроек();
	
	Попытка
		нсиСохранениеНастроекСписков.ДесериализоватьОтбор(Параметры.Отбор,КомпоновщикНастроек.Настройки.Отбор);
	Исключение
	КонецПопытки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Закрыть(СформироватьРезультат());
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипПредметаПриИзменении(Элемент)
	ИнициализироватьКомпоновщикНастроек();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьРезультат()
	Возврат новый Структура(
		"ТипПредмета,Представление,Отбор",
		ТипПредмета,
		Строка(КомпоновщикНастроек.Настройки.Отбор),
		нсиСохранениеНастроекСписков.СериализоватьОтбор(КомпоновщикНастроек.Настройки.Отбор)
	);
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	Если НЕ ЗначениеЗаполнено(ТипПредмета) Тогда 
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка КАК БизнесПроцесс ИЗ БизнесПроцесс."+Параметры.ИмяБП;
	Иначе
		Если ТипЗнч(ТипПредмета) = Тип("СправочникСсылка.нсиВидыСправочников") Тогда 
			пМетаданные = нсиУниверсальноеХранилище.ПолучитьМетаданные(ТипПредмета);
			
			ДопСвязи = 
				"ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс."+Параметры.ИмяБП+" БП 
				|	ПО БП.Предмет = ОсновнаяТаблица.Ссылка";
			
			ТекстЗапроса = нсиУниверсальноеХранилищеФормыСервер.ПолучитьТекстЗапроса(
				пМетаданные,
				"БП.Ссылка КАК БизнесПроцесс",
				ДопСвязи
			);
			
		Иначе
			ТекстЗапроса = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ 
				|	Ссылка КАК БизнесПроцесс,
				|	ВЫРАЗИТЬ(Ссылка.Предмет КАК Справочник."+ТипПредмета+") КАК Предмет
				|ИЗ 
				|	БизнесПроцесс."+Параметры.ИмяБП;
		КонецЕсли;
	КонецЕсли;
	СхемаКомпоновкиДанных 	= Новый СхемаКомпоновкиДанных;
	
	// новый источник данных
	ИсточникиДанных 	= СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	ИсточникиДанных.Имя 				= "ИсточникиДанных1";
	ИсточникиДанных.ТипИсточникаДанных 	= "Local";
	
	// новый запрос по источнику
	НаборДанныхЗапрос 	= СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанныхЗапрос.ИсточникДанных 	= "ИсточникиДанных1";
	НаборДанныхЗапрос.Имя 				= "Запрос";
	НаборДанныхЗапрос.Запрос 			= ТекстЗапроса;
	
	АдресКомпоновки = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресКомпоновки));
КонецПроцедуры

#КонецОбласти


