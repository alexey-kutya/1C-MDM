#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	#Если ТолстыйКлиентОбычноеПриложение Тогда 
		ПоказатьПредупреждение(,
			"Работа с формой элемента справочника 
			|""Настройки редактирования макетов БП""
			|недоступна в режиме обычного приложения!");
		Отказ = Истина;
		Возврат;
	#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСправочники


&НаКлиенте
Процедура СправочникиПриАктивизацииСтроки(Элемент)
	
	ТекСтрока = Элементы.Справочники.ТекущиеДанные;
	Если не ТекСтрока = Неопределено Тогда
		Элементы.Реквизиты.ОтборСтрок = Новый ФиксированнаяСтруктура("ИДРек", ТекСтрока.ИД);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СправочникиПередУдалением(Элемент, Отказ)
	
	ТекСтрока = Элементы.Справочники.ТекущиеДанные;
	Отбор = Новый Структура("ИДрек", ТекСтрока.ИД);
	Строки = Объект.Реквизиты.НайтиСтроки(Отбор);
	
	Для каждого стр из Строки цикл
   		Объект.Реквизиты.Удалить(стр);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СправочникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элементы.Справочники.ТекущиеДанные.ИД = Новый УникальныйИдентификатор;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СправочникиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда 
		нсиРаботаСФормамиКлиент.ПроверитьУникальностьСтроки(
			Элементы.Справочники,
			Объект.Справочники,
			"Наименование",
			ОтменаРедактирования,
			Отказ,
			"Справочник с таким именем уже введен!"
		);
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура СправочникиНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокЗн = ПолучитьСписокСправочников();
	
	ВыбЭлемент = СписокЗн.ВыбратьЭлемент();
	Если не ВыбЭлемент = Неопределено Тогда
		Элементы.Справочники.ТекущиеДанные.Наименование = ВыбЭлемент.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекСтрока = Элементы.Справочники.ТекущиеДанные;
	Если не ТекСтрока = Неопределено Тогда
		Элементы.Реквизиты.ОтборСтрок = Новый ФиксированнаяСтруктура("ИДРек", ТекСтрока.ИД);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеквизиты

&НаКлиенте
Процедура РеквизитыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Элементы.Справочники.ТекущаяСтрока = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не выбран справочник!",,,,Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РеквизитыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.ИдРек = Элементы.Справочники.ТекущиеДанные.ИД;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РеквизитыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекСтрока = Элементы.Реквизиты.ТекущиеДанные;
	Отбор = Новый Структура("ИмяРеквизита, ИДРек",ТекСтрока.ИмяРеквизита, ТекСтрока.ИДРек);
	МассивСтрок = Объект.Реквизиты.НайтиСтроки(Отбор);
	
	Если МассивСтрок.Количество() > 1 Тогда
		ТекстСообщения = "Поле формы " + ТекСтрока.ИмяРеквизита + " уже указано!";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Объект.Реквизиты.Удалить(ТекСтрока);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подбор(Команда)
	ТД = Элементы.Справочники.ТекущиеДанные;
	Если ТД<>Неопределено Тогда
		ОписаниеОповещения = новый ОписаниеОповещения("ОбработкаЗакрытияФормыПодбораЭлементов",ЭтаФорма);
		ОткрытьФорму(
			"Справочник.нсиБлокировкаРеквизитов.Форма.ФормаПодбораЭлементов",
			новый Структура("ИмяСправочника",ТД.Наименование),ЭтаФорма,,,,
			ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
		);
	Иначе
		ПоказатьПредупреждение(,"Не выбран справочник!");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияФормыПодбораЭлементов(Результат,ДП) Экспорт
	Если Результат<>Неопределено Тогда 
		ТД = Элементы.Справочники.ТекущиеДанные;
		Для Каждого Э Из Результат Цикл
			Строки = Объект.Реквизиты.НайтиСтроки(новый Структура("ИДРек,ИмяРеквизита",ТД.ИД,Э.Значение));
			Если Строки.Количество()=0 Тогда 
				НС = Объект.Реквизиты.Добавить();
				НС.ИмяРеквизита = Э.Значение;
				НС.ИДРек = ТД.ИД;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокСправочников()
	
	ВозвращСписок = Новый СписокЗначений;
	нсиБизнесПроцессы.ЗаполнитьСписокСправочниковВедущихсяПоЗаявкам(ВозвращСписок);
	Возврат ВозвращСписок;
	
КонецФункции

#КонецОбласти