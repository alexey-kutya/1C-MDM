#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ИмяСправочника.СписокВыбора.Добавить("нсиУслуги",   		"Услуги");
	Элементы.ИмяСправочника.СписокВыбора.Добавить("нсиКонтрагенты",   	"Контрагенты");
	Элементы.ИмяСправочника.СписокВыбора.Добавить("нсиМТР",   			"Материально-технические ресурсы");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	нсиВидыСправочников.Ссылка,
		|	нсиВидыСправочников.Наименование
		|ИЗ
		|	Справочник.нсиВидыСправочников КАК нсиВидыСправочников
		|ГДЕ
		|	НЕ нсиВидыСправочников.ПометкаУдаления
		|	И нсиВидыСправочников.ВидСправочника В (ЗНАЧЕНИЕ(Перечисление.нсиВидыСправочников.ФункциональныйСправочник), ЗНАЧЕНИЕ(Перечисление.нсиВидыСправочников.Классификатор))"
	);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Элементы.ИмяСправочника.СписокВыбора.Добавить(Выборка.Ссылка,Выборка.Наименование);
	КонецЦикла;
	
	Элементы.ИмяСправочника.СписокВыбора.СортироватьПоПредставлению();
	
	Если не ЗначениеЗаполнено(Объект.ИмяСправочника) Тогда 
		Объект.ИмяСправочника = Параметры.ИмяСправочника;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(Объект.ИмяСправочника) Тогда 
		ЗаполнитьФорму(Параметры);
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ПараметрыНеточногоПоиска = Новый ХранилищеЗначения(
		ПараметрыНеточногоПоиска.Выгрузить());
	ТекущийОбъект.НастройкаКомпоновщика 	= Новый ХранилищеЗначения(
		КомпоновщикНастроек.ПолучитьНастройки());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяСправочникаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ИмяСправочника) Тогда 
		ЗаполнитьФорму();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПараметрыНеточногоПоиска

&НаКлиенте
Процедура ПараметрыНеточногоПоискаПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	// Обработчик подсистемы "Сравнение данных"
	нсиСравнениеДанныхКлиент.ПараметрыНеточногоПоискаПеретаскивание(Объект.ИмяСправочника, 
		ПараметрыНеточногоПоиска, ПараметрыПеретаскивания); 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	ПередаваемыеПараметры = Новый Структура;
	ПередаваемыеПараметры.Вставить("Ссылка",				Объект.Ссылка);
	ПередаваемыеПараметры.Вставить("НастройкаПоиска",		КомпоновщикНастроек.Настройки);
	ПередаваемыеПараметры.Вставить("ПараметрыПоиска",		ПараметрыНеточногоПоиска);
	ЭтаФорма.Закрыть(ПередаваемыеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФорму(ВходящиеДанные = Неопределено)
	
	// Обработчик подсистемы "Сравнение данных"
	нсиСравнениеДанныхСервер.ЗаполнениеКомпоновщикаНастроек(Объект.ИмяСправочника, 
		АдресКомпоновки, КомпоновщикНастроек, ЭтаФорма.УникальныйИдентификатор);
	
	Если ВходящиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	
	пЭтотОбъект = РеквизитФормыВЗначение("Объект");
	
	// заполняются по сохраненным данным
	Если ЗначениеЗаполнено(пЭтотОбъект.ПараметрыНеточногоПоиска.Получить()) Тогда 
		ПараметрыНеточногоПоиска.Загрузить(пЭтотОбъект.ПараметрыНеточногоПоиска.Получить());   
	КонецЕсли;
	Если ЗначениеЗаполнено(пЭтотОбъект.НастройкаКомпоновщика.Получить()) Тогда 
		КомпоновщикНастроек.ЗагрузитьНастройки(пЭтотОбъект.НастройкаКомпоновщика.Получить());
	КонецЕсли;
	
	// заполняются по полученным данным
	Если ЗначениеЗаполнено(Параметры.ПараметрыПоиска) Тогда 
		ПараметрыНеточногоПоиска.Загрузить(Параметры.ПараметрыПоиска.Выгрузить());   
	КонецЕсли;
	Если ЗначениеЗаполнено(Параметры.НастройкаПоиска) Тогда 
		КомпоновщикНастроек.ЗагрузитьНастройки(Параметры.НастройкаПоиска);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти






