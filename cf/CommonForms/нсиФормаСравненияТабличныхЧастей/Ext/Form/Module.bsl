
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	
	// Цвета условного оформления.
	ЦветЗеленый = Новый Цвет(0,150,70);
	ЦветСиний   = Новый Цвет(51,102,255);
	ЦветКрасный = Новый Цвет(255,0,0);
	
	тзСравнения = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого Колонка из тзСравнения.Колонки цикл
		НовыйРеквизит = Новый РеквизитФормы(Колонка.Имя, Колонка.ТипЗначения, "ТаблицаСравнения", Колонка.Заголовок);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	КонецЦикла;	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ТаблицаСравнения.Загрузить(тзСравнения);
	
	ПоляОригинала = Новый Массив;
	ПоляМакета    = Новый Массив;
	
	Для Каждого ДобавляемыйРеквизит из ДобавляемыеРеквизиты цикл
		Если СтрНайти(ДобавляемыйРеквизит.Имя, "Оригинал") > 0
			ИЛИ ДобавляемыйРеквизит.Имя = "ВариантИзменения"
			ИЛИ ДобавляемыйРеквизит.Имя = "ИсходныйНомерСтроки" тогда
			Продолжить;
		КонецЕсли;	
		
		ПутьКДанным         = "ТаблицаСравнения." + ДобавляемыйРеквизит.Имя;
		ПутьКДаннымОригинал = "ТаблицаСравнения." + ДобавляемыйРеквизит.Имя + "Оригинал";
		
		Реквизит         = ДобавляемыйРеквизит.Имя;
		РеквизитОригинал = ДобавляемыйРеквизит.Имя + "Оригинал";
		
		НоваяГруппаКолонок = Элементы.Добавить("ГруппаКолонок" + ДобавляемыйРеквизит.Имя, Тип("ГруппаФормы"), Элементы.ТаблицаСравнения);
				
		НовыйЭлемент = Элементы.Добавить(Реквизит, Тип("ПолеФормы"), НоваяГруппаКолонок);
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
		НовыйЭлемент.Вид         = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = Колонка.Заголовок;
		
		ЕстьРеквизитОригинал = НЕ тзСравнения.Колонки.Найти(РеквизитОригинал) = Неопределено;
		
		Если ЕстьРеквизитОригинал тогда
			НовыйЭлемент = Элементы.Добавить(РеквизитОригинал, Тип("ПолеФормы"), НоваяГруппаКолонок);
			НовыйЭлемент.ПутьКДанным = ПутьКДаннымОригинал;
			НовыйЭлемент.Вид         = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.Заголовок = Колонка.Заголовок;
			
			// Элемент условного оформления Измененные.
			НовыйЭлемент = УсловноеОформление.Элементы.Добавить();
			
			Поле = НовыйЭлемент.Поля.Элементы.Добавить();
			Поле.Поле          = Новый ПолеКомпоновкиДанных(Реквизит);
			Поле.Использование = Истина;
			
			Поле = НовыйЭлемент.Поля.Элементы.Добавить();
			Поле.Поле          = Новый ПолеКомпоновкиДанных(РеквизитОригинал);
			Поле.Использование = Истина;
			
			ГруппаОтбора = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
			ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКДанным);
			ЭлементОтбора.ПравоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДаннымОригинал);
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
			ЭлементОтбора.Использование  = Истина;
			
			ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаСравнения.ВариантИзменения");
			ЭлементОтбора.ПравоеЗначение = 1;
			ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбора.Использование  = Истина;
			
			НовыйЭлемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветКрасный);
			
			ПоляОригинала.Добавить(Новый ПолеКомпоновкиДанных(РеквизитОригинал));
		КонецЕсли;
		ПоляМакета.Добавить(Новый ПолеКомпоновкиДанных(Реквизит));
	КонецЦикла;	
	
	// Элемент условного оформления В макете.
	НовыйЭлемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого текПоле из ПоляМакета цикл
		Поле = НовыйЭлемент.Поля.Элементы.Добавить();
		Поле.Поле          = текПоле;
		Поле.Использование = Истина;
	КонецЦикла;
	
	ЭлементОтбора = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаСравнения.ВариантИзменения");
	ЭлементОтбора.ПравоеЗначение = 2;
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование  = Истина;
	
	НовыйЭлемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветЗеленый);
	
	// Элемент условного оформления В оригинале.
	НовыйЭлемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого текПоле из ПоляОригинала цикл
		Поле = НовыйЭлемент.Поля.Элементы.Добавить();
		Поле.Поле          = текПоле;
		Поле.Использование = Истина;
	КонецЦикла;
	
	ЭлементОтбора = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаСравнения.ВариантИзменения");
	ЭлементОтбора.ПравоеЗначение = 3;
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование  = Истина;
	
	НовыйЭлемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветСиний);
	
КонецПроцедуры

#КонецОбласти
